;; build.ss — `jolt build`: AOT-compile an app into a standalone executable.
;;
;; Loaded on demand by cli.ss when the command is `build`. Defines the host
;; primitive jolt.host/build-binary, which jolt.main's build command calls after
;; resolving the project's deps + source roots.
;;
;; The pipeline (Phase 4 stage 2):
;;   1. load the entry namespace — registers its macros/vars and follows requires,
;;      recording the app namespaces in dependency order (loader's ns-loaded-hook).
;;   2. re-emit each app namespace to Scheme (the emit-image cross-compile path),
;;      now that its macros are registered.
;;   3. textually inline the cli.ss runtime load sequence into one flat source,
;;      append the app emission + a launcher that calls the entry's -main.
;;   4. compile-file -> make-boot-file -> embed the boot as C bytes -> cc-link
;;      against libkernel.a into a single self-contained binary.
;;
;; emit-image.ss supplies the cross-compiler (ei-* helpers); it's loaded here so a
;; normal run never pays for it.

(load "host/chez/emit-image.ss")
(load "host/chez/dce.ss")

;; --- shell helpers ----------------------------------------------------------
;; Run a command, return its stdout as one trimmed string ("" on no output).
(define (bld-sh-capture cmd)
  (let* ((p (process (bld-sh-wrap cmd))) (in (car p)))
    (let loop ((acc '()))
      (let ((l (get-line in)))
        (if (eof-object? l)
            (begin (close-port in)
                   ;; rejoin with newlines (get-line stripped them). Callers use
                   ;; single-line output; this just avoids silently concatenating
                   ;; two lines into one corrupt token if a command emits more.
                   (let ((ls (reverse acc)))
                     (if (null? ls) ""
                         (fold-left (lambda (s x) (string-append s "\n" x)) (car ls) (cdr ls)))))
            (loop (cons l acc)))))))

(define (bld-system cmd)
  (let ((rc (system (bld-sh-wrap cmd))))
    (unless (zero? rc)
      (error 'jolt-build (string-append "command failed (" (number->string rc) "): " cmd)))))

;; mkdir -p without a subprocess (the self-contained build shells out to nothing).
(define (bld-mkdir-p dir)
  (unless (or (string=? dir "") (string=? dir "/") (string=? dir ".") (file-exists? dir))
    (bld-mkdir-p (path-parent dir))
    ;; tolerate only the benign race (someone else created it) — a real mkdir
    ;; failure (permissions) used to surface later as a less specific
    ;; open-output-file error.
    (guard (e (#t (unless (file-exists? dir) (raise e))))
      (mkdir dir))))

(define (bld-contains? s sub)
  (let ((ns (string-length s)) (nsub (string-length sub)))
    (let loop ((i 0))
      (cond ((> (+ i nsub) ns) #f)
            ((string=? (substring s i (+ i nsub)) sub) #t)
            (else (loop (+ i 1)))))))

;; Shell-quote a path: wrap in single quotes. Paths in this project are assumed
;; to not contain single quotes (which would break the quoting).
(define (bld-sh-quote s)
  (string-append "'" s "'"))

;; --- toolchain discovery ----------------------------------------------------
;; bld-machine / bld-osx? / bld-nt? describe the HOST — the machine the build
;; RUNS on (shell wrapping, cc discovery, loading a native archive into the build
;; process). Where a decision is about the OUTPUT binary instead (link libs, the
;; boots, the .exe/.dylib suffix, symbol export), use the target-aware predicates
;; below so `jolt build --target <machine>` cross-compiles correctly.
(define bld-machine (sa-host-tag))
(define bld-osx? (eq? (sa-os-family) 'macos))
(define bld-nt? (eq? (sa-os-family) 'windows))

;; The target machine: #f = build for the host; a Chez machine string
;; ("ta6osx", "tarm64le", "ta6nt", …) = cross-compile. Set by jolt build --target.
(define bld-target (make-parameter #f))
;; A prepared target pack: a directory holding the target's petite.boot,
;; scheme.boot, libkernel.a and scheme.h (the csv layout), the cross xpatch, a
;; `link-libs` file with the target link flags, and static lz4/zlib under lib/.
;; Required when bld-target is set. Produced by the one-time ChezScheme cross
;; setup — see tools/cross-compile/README.md.
(define bld-target-pack (make-parameter #f))
;; The effective target machine, and whether this is a cross build.
(define (bld-eff-machine) (or (bld-target) bld-machine))
(define (bld-cross?) (and (bld-target) (not (string=? (bld-target) bld-machine)) #t))
;; A Darwin target says "osx" OR "ios": Chez's four iOS tags (a6ios, arm64ios,
;; ta6ios, tarm64ios) name Darwin without saying "osx" — the same vocabulary
;; sa-os-family-for-tag matches on. Without "ios" here, `jolt build --target
;; tarm64ios --library` links the output with ELF's -shared instead of
;; -dynamiclib -install_name, which cannot produce a loadable Darwin dylib.
(define (bld-tgt-osx?) (or (bld-contains? (bld-eff-machine) "osx")
                           (bld-contains? (bld-eff-machine) "ios")))
(define (bld-tgt-nt?) (bld-contains? (bld-eff-machine) "nt"))
;; An env override, treating an EMPTY value as absent. A Makefile that exports a
;; variable it did not manage to compute exports "" rather than nothing, and ""
;; is a true value in Scheme -- so a plain (or (getenv ...) "cc") would hand the
;; link an empty compiler name and fail somewhere far from the cause.
(define (bld-env-override name)
  (let ((v (getenv name))) (and v (> (string-length v) 0) v)))

;; The C compiler + arch flag for the OUTPUT binary. Cross overrides via env
;; (JOLT_TARGET_CC, e.g. aarch64-linux-gnu-gcc or a zig-cc wrapper;
;; JOLT_TARGET_ARCH_FLAG, e.g. "-arch x86_64" for a macOS x-arch link).
;;
;; JOLT_CC names the compiler for a NATIVE link. It exists because a bare `cc`
;; is resolved by PATH, and PATH is not neutral when make provisions the pinned
;; toolchain: gcc.mk puts the xPack bundle's bin directory FIRST on an exported
;; PATH, so `as` and `ld` come from that bundle -- but the bundle ships no `cc`,
;; so `cc` alone falls through to the system compiler. The link then pairs one
;; vendor's driver with another vendor's assembler and linker (#788: a distro
;; gcc 16 emitting .base64 into the bundle's pre-2.43 gas). Chez itself never
;; had the problem: chezscheme.mk builds it with the absolute CC=$(GCC), and the
;; Makefile now exports JOLT_CC with that same GCC. Same shape as JOLT_CHEZ,
;; which exists because a bare `chez` on PATH could likewise be a different
;; install from the one make provisioned.
(define (bld-cc)
  (if (bld-cross?)
      (or (bld-env-override "JOLT_TARGET_CC") "cc")
      (or (bld-env-override "JOLT_CC") "cc")))
(define (bld-arch-flag) (if (bld-cross?) (or (getenv "JOLT_TARGET_ARCH_FLAG") "") ""))

;; Platform-appropriate flag to export executable symbols so a statically-linked
;; native lib's symbols resolve via (load-shared-object #f). macOS keeps unstripped
;; dlsym visibility; Windows needs an explicit export table; ELF (Linux) needs -rdynamic.
(define (bld-export-symbols-flag)
  (cond (bld-osx? "")
        (bld-nt? "-Wl,--export-all-symbols ")
        (else "-rdynamic ")))

;; Chez's system/process run through cmd.exe on Windows; every build command
;; here is written for sh (MSYS2 provides it). On nt, spill the command to a
;; script and run `sh <file>` — workspace paths carry no spaces, and the
;; script file sidesteps cmd's quoting entirely. Identity elsewhere.
(define bld-shell-counter 0)
;; On nt, spill the command to a script and run `sh <file>`. Chez has no getpid,
;; so per-process uniqueness comes from first-use millis + a counter (the same
;; scheme spit's temp files use) — concurrent builds sharing TEMP don't collide.
;; The stamp resolves lazily: jolt bakes this file into a saved heap, and a
;; load-time stamp would freeze identical across every process run from it.
;; Delete the script on success; leave it on failure for debugging.
(define bld-shell-stamp #f)
(define (bld-sh-wrap cmd)
  (if bld-nt?
      (let* ((stamp (or bld-shell-stamp
                        (let ((s (number->string (sa-real-time-ms))))
                          (set! bld-shell-stamp s) s)))
             (tmp (or (getenv "TEMP") (getenv "TMP") "."))
             (f (begin (set! bld-shell-counter (+ bld-shell-counter 1))
                       (string-append tmp "\\jolt-sh-" stamp "-"
                                      (number->string bld-shell-counter) ".sh"))))
        (let ((p (open-output-file f 'replace)))
          (put-string p cmd)
          (close-port p))
        (let ((qf (string-append "'"
                    (apply string-append
                      (map (lambda (c) (if (char=? c #\') "'\\''" (string c)))
                           (string->list f)))
                    "'")))
          (string-append "sh " qf " && rm -f " qf)))
      cmd))

;; The directory holding an executable named the way the shell would find it. A
;; BARE command name — JOLT_CHEZ=scheme, which runs perfectly well since PATH is
;; what locates it — has no directory part to take, and path-parent answers "" for
;; it where dirname answers ".": unhandled, the csv candidate built from it became
;; an absolute "/../lib/csv<ver>" off the filesystem root. PATH is the only thing
;; that knows where such a name lives, so ask it.
(define (bld-exe-dir exe)
  (let ((parent (path-parent exe)))
    (if (string=? parent "")
        (let ((p (bld-sh-capture
                  (string-append "dirname \"$(command -v " (bld-sh-quote exe) ")\""))))
          (if (> (string-length p) 0) p "."))
        parent)))

;; The Chez executable, for the isolated compile pass (see build-binary step 4).
;; $JOLT_CHEZ — the interpreter this script is itself running under — is
;; authoritative when set, for the same reason as bld-host-csv-dir above: a
;; fresh `command -v` PATH search can silently name a different Chez than the
;; one actually selected to run the build.
(define bld-chez
  (let ((env (getenv "JOLT_CHEZ")))
    (if (and env (> (string-length env) 0))
        env
        (let ((p (bld-sh-capture "command -v chez || command -v scheme || command -v petite")))
          (if (> (string-length p) 0) p "chez")))))

;; Chez version off (scheme-version) "Chez Scheme Version X.Y.Z" — last token.
(define bld-version
  (let* ((s (scheme-version)) (n (string-length s)))
    (let loop ((i n))
      (if (or (= i 0) (char=? (string-ref s (- i 1)) #\space))
          (substring s i n)
          (loop (- i 1))))))

;; The HOST csv<ver>/<machine> dir holding scheme.h, libkernel.a, *.boot. Derived
;; from the chez executable's location; JOLT_CHEZ_CSV overrides.
;;
;; The executable's location comes from $JOLT_CHEZ (the same interpreter this
;; script is running under — bin/jolt exports it) when set, rather than a fresh
;; `command -v chez/scheme/petite` PATH search: that search re-derives an
;; independent answer, which silently disagrees with the running interpreter
;; whenever the one actually selected (make's local .cache/local provision, or
;; any JOLT_CHEZ override) isn't also the one PATH would resolve — producing a
;; mismatched dir (e.g. a PATH-resolved 9.x scheme paired with bld-version read
;; off the running 10.x interpreter) that then fails bld-check-toolchain. The
;; PATH search remains the fallback for a chez invoked directly, with no
;; JOLT_CHEZ in its environment at all.
(define bld-host-csv-dir
  (let ((env (getenv "JOLT_CHEZ_CSV")))
    (or (and env (> (string-length env) 0) env)
        (let* ((chez (getenv "JOLT_CHEZ"))
               (bindir (if (and chez (> (string-length chez) 0))
                           (bld-exe-dir chez)
                           (bld-sh-capture "dirname \"$(command -v chez || command -v scheme || command -v petite)\"")))
               (cand (string-append bindir "/../lib/csv" bld-version "/" bld-machine)))
          cand))))
;; The csv dir supplying the boots + kernel + scheme.h that get baked into the
;; OUTPUT binary: the target pack when cross-compiling, else the host csv. (For a
;; cross build the host chez still runs the compile, finding its own boots via its
;; install; only make-boot-file / the kernel link read the target's, via the pack.)
(define (bld-csv-dir) (if (bld-cross?) (bld-target-pack) bld-host-csv-dir))
;; The cross xpatch that retargets compile-file / make-boot-file to the target.
(define (bld-xpatch) (string-append (bld-target-pack) "/xpatch"))

(define (bld-have-cc?)
  (> (string-length (bld-sh-capture "command -v cc")) 0))

(define (bld-check-toolchain)
  (let ((hint (if (bld-cross?)
                  "\nProvide a target pack (--target-pack DIR) — see tools/cross-compile/README.md."
                  "\nSet JOLT_CHEZ_CSV to the csv<ver>/<machine> dir.")))
    (for-each
      (lambda (f)
        (let ((p (string-append (bld-csv-dir) "/" f)))
          (unless (file-exists? p)
            (error 'jolt-build (string-append "Chez build file missing: " p hint)))))
      '("scheme.h" "libkernel.a" "petite.boot" "scheme.boot"))
    ;; a cross pack additionally supplies the xpatch and the target link flags.
    (when (bld-cross?)
      (unless (bld-target-pack)
        (error 'jolt-build "cross build (--target) needs a target pack (--target-pack DIR)"))
      (for-each
        (lambda (f)
          (let ((p (string-append (bld-target-pack) "/" f)))
            (unless (file-exists? p)
              (error 'jolt-build (string-append "target pack file missing: " p hint)))))
        '("xpatch" "link-libs")))))

;; The kernel's compression libraries — lz4 and zlib — as absolute paths to
;; STATIC archives, or #f when none is reachable. Chez compiles both as part of
;; its own build and installs liblz4.a / libz.a next to libkernel.a, so the
;; archives matching the kernel this link bakes in are normally already in the csv
;; dir, on every platform — Homebrew's chezscheme included (it vendors them; it
;; depends on neither formula). The keg and pkg-config are macOS fallbacks for a
;; Chez that somehow installed without them.
;;
;; Naming an archive by path is what forces the static choice: Apple's ld has no
;; -Bstatic, and a bare -llz4 pointed at a directory holding both a .dylib and a
;; .a always takes the .dylib. That is how the released macOS binary came to
;; demand /opt/homebrew/opt/lz4/lib/liblz4.1.dylib off every machine that ran it
;; — the install script's own `jolt --version` check died with a dyld error on a
;; Mac with no Homebrew lz4, which is most of them. Neither library has to be a
;; dependency of anything jolt produces: a jolt binary is meant to run with
;; nothing else installed, the way a Go binary does.

;; Archives the caller has already put on disk, as (("lz4" . path) ("z" . path)).
;; They outrank the search below: the self-contained jolt has no Chez install to
;; look in, so it carries the archives its own kernel was linked against and
;; spills them for the one link it still performs (bld-relink-stub).
(define bld-bundled-archives (make-parameter '()))

;; lib name -> (Homebrew keg, pkg-config module), for the macOS fallbacks.
(define bld-archive-sources '(("lz4" "lz4" "liblz4") ("z" "zlib" "zlib")))

(define (bld-static-archive lib)
  (let ((file (string-append "lib" lib ".a"))
        (src (assoc lib bld-archive-sources)))
    (let loop ((thunks
                 (cons*
                   (lambda () (cond ((assoc lib (bld-bundled-archives)) => cdr) (else #f)))
                   ;; The csv dir the kernel itself comes from. (bld-csv-dir), not
                   ;; bld-host-csv-dir, for the reason the Linux branch gives
                   ;; below. A cross build's link never asks (the pack's link-libs
                   ;; carries its own -llz4 -lz, resolved against the archives
                   ;; under lib/), but build-jolt does, to embed them — hence the
                   ;; pack path.
                   (lambda () (string-append (bld-csv-dir) "/" file))
                   (lambda () (and (bld-cross?)
                                   (string-append (bld-target-pack) "/lib/" file)))
                   ;; macOS only. A distro's static archive is deliberately NOT
                   ;; searched on Linux: it may well be non-PIC, which would turn
                   ;; today's working `jolt build --library` into a link error
                   ;; ("recompile with -fPIC"). Chez's own archives are built
                   ;; alongside libkernel.a, which that link already folds into a
                   ;; shared object, so they carry the same guarantee. Darwin
                   ;; compiles PIC throughout, so neither fallback has the problem.
                   (if (and bld-osx? src)
                       (list
                         (lambda ()
                           (let ((prefix (bld-sh-capture
                                           (string-append "brew --prefix " (cadr src) " 2>/dev/null"))))
                             (and (> (string-length prefix) 0)
                                  (string-append prefix "/lib/" file))))
                         (lambda ()
                           (let ((libdir (bld-sh-capture
                                           (string-append "pkg-config --variable=libdir "
                                             (caddr src) " 2>/dev/null"))))
                             (and (> (string-length libdir) 0)
                                  (string-append libdir "/" file)))))
                       '()))))
      (if (null? thunks)
          #f
          (let ((path ((car thunks))))
            (if (and path (file-exists? path)) path (loop (cdr thunks))))))))

;; No archive anywhere: the link still has to find SOMETHING, and a binary that
;; needs a shared library beats one that does not link at all. Say so — the
;; result runs here and nowhere else, which is not what a `jolt build` output is
;; for.
(define (bld-warn-dynamic-lib! lib)
  (display (string-append
    "jolt build: warning: no static lib" lib ".a found next to the Chez kernel\n"
    "  — linking " lib " dynamically; the binary will need it on every machine that runs it\n")))

;; The macOS -llz4 fallback: the keg (or pkg-config) at least tells the linker
;; where the dylib is, which a bare -llz4 on a Mac with no lz4 in /usr/lib cannot.
(define (bld-osx-lz4-dynamic)
  (bld-warn-dynamic-lib! "lz4")
  (let ((prefix (bld-sh-capture "brew --prefix lz4 2>/dev/null")))
    (if (> (string-length prefix) 0)
        (string-append "-L" (bld-sh-quote (string-append prefix "/lib")) " -llz4")
        (let ((pc (bld-sh-capture "pkg-config --libs-only-L liblz4 2>/dev/null")))
          (if (> (string-length pc) 0)
              (string-append pc " -llz4")
              "-llz4")))))

;; The link fragment for one of the kernel's compression libraries: the archive's
;; path when one is reachable, else -l<lib>. WARN? says whether the fallback is
;; worth a word. It always is for lz4, which no OS ships, and on Linux for zlib;
;; on macOS libz is /usr/lib/libz.1.dylib, as much a part of the OS as libiconv,
;; so falling back to it there is unremarkable.
(define (bld-compression-lib lib warn?)
  (let ((archive (bld-static-archive lib)))
    (cond
      (archive (string-append (bld-sh-quote archive) " "))
      ;; a bare -llz4 finds nothing on a Mac: the keg is not on the default path.
      ((and bld-osx? (string=? lib "lz4")) (string-append (bld-osx-lz4-dynamic) " "))
      (else (when warn? (bld-warn-dynamic-lib! lib))
            (string-append "-l" lib " ")))))

;; Link flags. The kernel's lz4/zlib/ncurses deps, lz4 statically (see above).
;; The host branches double as the target flags for a non-cross build
;; (host = target).
(define (bld-link-libs)
  (cond
    ;; cross: the static lz4/zlib live in the pack (lib/), and the pack's
    ;; `link-libs` file lists the remaining -l/-framework flags — which depend on
    ;; how its kernel was configured (a cross kernel is often --disable-curses /
    ;; --disable-x11). JOLT_TARGET_LINK_LIBS overrides the whole string.
    ((bld-cross?)
     (or (getenv "JOLT_TARGET_LINK_LIBS")
         (string-append "-L" (bld-sh-quote (string-append (bld-target-pack) "/lib")) " "
           (bld-sh-capture (string-append "cat " (bld-sh-quote (string-append (bld-target-pack) "/link-libs")))))))
    ;; macOS: libncurses, libiconv and Foundation ship with the OS and stay
    ;; dynamic — they cannot be baked in. lz4 and zlib can, and are: lz4 because
    ;; the OS has none at all, zlib because the archive Chez built is right there
    ;; next to the kernel, which leaves the dependency list to things that are
    ;; genuinely part of macOS.
    (bld-osx?
     (string-append
       (bld-compression-lib "lz4" #t)
       (bld-compression-lib "z" #f)
       "-lncurses -framework Foundation -liconv -lm"))
    ;; Windows (ta6nt, MinGW-w64 under MSYS2): the Chez kernel pulls in
    ;; compression, winsock, COM/UUID, and the registry.
    (bld-nt?
          ;; -static: a single-file exe (no libwinpthread/libgcc/lz4 DLL deps) —
     ;; required for a distributable binary and for TLS init consistency.
     "-static -llz4 -lz -lws2_32 -lrpcrt4 -lole32 -luuid -ladvapi32 -luser32 -lshell32 -lm")
    ;; Linux: the Chez kernel pulls in compression (lz4/z), the expression
    ;; editor (ncurses + terminfo), threads, dlopen, libuuid, and clock_gettime.
    ;;
    ;; --exclude-libs keeps the terminal libraries OUT of the executable's
    ;; dynamic symbol table. -rdynamic puts everything else in, which is what
    ;; lets a statically linked native resolve through (load-shared-object #f) —
    ;; but exporting ncurses is actively harmful. The executable is searched
    ;; before any dlopen'd library, so a jolt program that binds a real ncurses
    ;; through the FFI has that library's own calls (_nc_setupterm and the rest)
    ;; bound back into the kernel's copy, which is a different build with a
    ;; different TERMINAL layout: the terminfo entry fails to parse, or initscr
    ;; segfaults on the mismatch. Naming the archives costs nothing when they
    ;; resolve to shared libraries instead — ld ignores an --exclude-libs name
    ;; it did not link.
    (else
     (string-append
       ;; -L the csv dir the kernel itself comes from: Chez ships liblz4.a /
       ;; libz.a there, so -llz4 -lz resolve on a machine with no lz4/zlib
       ;; development packages. (bld-csv-dir), not bld-host-csv-dir — every
       ;; caller of bld-link-libs takes -I and libkernel.a from the same
       ;; place, which for a cross build is the TARGET pack, not this host.
       "-L" (bld-sh-quote (bld-csv-dir)) " "
       ;; liblz4.a/libz.a join the list for a milder version of the same reason:
       ;; the executable is searched before any dlopen'd library, so exporting a
       ;; baked-in deflate/LZ4_decompress means an FFI-loaded libpng, libssl or
       ;; libsqlite3 calls THIS copy instead of the one it was built against.
       ;; ld matches these by basename, so naming the archives by absolute path
       ;; above changes nothing here.
       "-Wl,--exclude-libs,libncurses.a:libncursesw.a:libtinfo.a:liblz4.a:libz.a "
       ;; lz4 and zlib by archive path rather than -llz4 -lz: the -L above
       ;; already preferred the csv archives over any system .so, but only as a
       ;; side effect of search order, so a Chez installed without them silently
       ;; produced a binary with runtime compression dependencies. Naming them
       ;; says so, and their absence is now a warning rather than silence. Falls
       ;; back to -l (the -L above, then LIBRARY_PATH, then the system dirs).
       (bld-compression-lib "lz4" #t)
       (bld-compression-lib "z" #t)
       "-lncurses -ltinfo -ldl -lm -lpthread -luuid -lrt"))))

;; --- optional built-binary startup profile ----------------------------------
;; JOLT_STARTUP_PROFILE=1 reports wall time, process CPU, collections,
;; reclaimed GC bytes, and current heap size at coarse runtime/app boundaries.
;; The definitions use Chez primitives only, so they can run before Jolt's runtime is initialized.
;; Calls stay in every built image but return immediately when the variable is
;; absent; this keeps one binary usable for normal runs and startup diagnosis.
(define (bld-emit-startup-profile-preamble out)
  (put-string out
    "(define jolt-startup-profile? (and (getenv \"JOLT_STARTUP_PROFILE\") #t))\n\
(define jolt-startup-profile-start-real\n\
  (and jolt-startup-profile? (real-time)))\n\
(define jolt-startup-profile-last-real jolt-startup-profile-start-real)\n\
(define jolt-startup-profile-last-cpu\n\
  (and jolt-startup-profile? (cpu-time)))\n\
(define jolt-startup-profile-initial-stats\n\
  (and jolt-startup-profile? (statistics)))\n\
(define jolt-startup-profile-last-collections\n\
  (and jolt-startup-profile?\n\
       (sstats-gc-count jolt-startup-profile-initial-stats)))\n\
(define jolt-startup-profile-last-gc-bytes\n\
  (and jolt-startup-profile?\n\
       (sstats-gc-bytes jolt-startup-profile-initial-stats)))\n\
(define (jolt-startup-profile-mark! label)\n\
  (when jolt-startup-profile?\n\
    (let* ((now-real (real-time))\n\
           (now-cpu (cpu-time))\n\
           (now-stats (statistics))\n\
           (now-collections (sstats-gc-count now-stats))\n\
           (now-gc-bytes (sstats-gc-bytes now-stats)))\n\
      (display\n\
        (string-append\n\
          \"jolt startup: [profile] scheme \" label\n\
          \"   wall \" (number->string (- now-real jolt-startup-profile-last-real)) \" ms\"\n\
          \"   cpu \" (number->string (- now-cpu jolt-startup-profile-last-cpu)) \" ms\"\n\
          \"   gc \" (number->string (- now-collections jolt-startup-profile-last-collections))\n\
          \"   gc-reclaimed \" (number->string (- now-gc-bytes jolt-startup-profile-last-gc-bytes)) \" bytes\"\n\
          \"   heap \" (number->string (current-memory-bytes)) \" bytes\"\n\
          \"   (cumulative \" (number->string (- now-real jolt-startup-profile-start-real)) \" ms)\\n\")\n\
        (current-error-port))\n\
      (let ((after-stats (statistics)))\n\
        (set! jolt-startup-profile-last-real (real-time))\n\
        (set! jolt-startup-profile-last-cpu (cpu-time))\n\
        (set! jolt-startup-profile-last-collections (sstats-gc-count after-stats))\n\
        (set! jolt-startup-profile-last-gc-bytes (sstats-gc-bytes after-stats))))))\n"))

(define (bld-startup-profile-form label)
  (string-append "(jolt-startup-profile-mark! " (ei-str-lit label) ")"))

(define (bld-emit-startup-profile-mark! out label)
  (put-string out (bld-startup-profile-form label))
  (put-string out "\n"))

(define (bld-runtime-entry-label entry)
  (cond
    ((symbol? entry) (symbol->string entry))
    ((bld-load-path entry) => (lambda (path) path))
    (else entry)))

;; --- runtime manifest (mirrors host/chez/cli.ss's load order) ---------------
;; A line is either literal Scheme text to inline, or a tag whose emission the build
;; controls: 'prelude (the clojure.core blob, replaced by the shaken core under
;; tree-shake), 'image + 'compile-eval (the compiler, dropped for a no-eval app).
;; Tagging keeps the splice/drop decisions off fragile substring matching.
(define bld-runtime-manifest
  (list
    ;; The runtime adapter loads FIRST: its sa-* entry points are referenced at
    ;; top level and inside macros during rt.ss's own load (rt.ss:51, and the
    ;; java/*.ss files rt.ss loads), so a later slot would resolve them
    ;; unbound. PSL R5+R6 pinned this order.
    "(load \"host/chez/scheme-adapter-runtime.ss\")"
    "(load \"host/chez/rt.ss\")"
    "(set-chez-ns! \"clojure.core\")"
    'prelude
    "(load \"host/chez/post-prelude.ss\")"
    "(load \"host/chez/post-prelude-str.ss\")"
    "(set-chez-ns! \"user\")"
    "(load \"host/chez/host-contract.ss\")"
    'image
    'compile-eval
    "(load \"host/chez/cli-core.ss\")"
    "(load \"host/chez/png.ss\")"
    "(load \"host/chez/loader.ss\")"
    "(load \"host/chez/diagnostic-render.ss\")"
    "(load \"host/chez/java/ffi.ss\")"
    (string-append "(set-source-roots! " (ldr-install-roots-str) ")")))

(define bld-tagged-loads
  '((prelude . "(load \"host/chez/seed/prelude.ss\")")
    (image . "(load \"host/chez/seed/image.ss\")")
    (compile-eval . "(load \"host/chez/compile-eval.ss\")")))

;; A single-line top-level `(load "PATH")` -> PATH, else #f. Only STRING-LITERAL
;; loads count (a `(load so)` runtime form must not be mistaken for a manifest
;; directive — it once tripped this into an error branch referencing an unbound
;; `die`). Bounded: each quote scan checks end-of-string; a missing close quote
;; yields #f (line not a recognized directive) rather than a crash.
(define (bld-load-path line)
  (let ((s (let trim ((i 0) (n (string-length line)))
             (if (and (< i n) (memv (string-ref line i) '(#\space #\tab)))
                 (trim (+ i 1) n)
                 (if (< i n) (substring line i n) "")))))
    (and (>= (string-length s) 8)                 ; "(load \"" minimum
         (string=? (substring s 0 6) "(load ")
         (char=? (string-ref s 6) #\")            ; only string-literal loads
         (let ((end (string-length s)))
           (let ((q2 (let scan ((i 7))
                       (if (>= i end) #f
                           (if (char=? (string-ref s i) #\") i (scan (+ i 1)))))))
             (and q2 (substring s 7 q2)))))))

;; runtime source for PATH: from the binary's embedded store if present (a
;; self-contained jolt building an app, with no jolt checkout on disk), else read
;; from disk (running from a source checkout). build-jolt embeds every runtime
;; .ss the manifest inlines, so `build` never touches the filesystem for them.
(define (bld-source-string path)
  (let ((emb (embedded-resource-ref path)))
    (cond ((string? emb) emb)
          ;; source embeds are UTF-8 bytevectors since the heap-size work —
          ;; missing this arm sent the standalone binary's `build` to disk for
          ;; host/chez/*.ss, which only exists inside a checkout (v0.4.0
          ;; release-smoke failure on all three platforms).
          ((bytevector? emb) (utf8->string emb))
          (else (read-file-string path)))))

(define (bld-string-lines s)
  ;; a line drops its trailing \r: a CRLF checkout (Windows git autocrlf) must
  ;; parse identically to an LF one — the stdlib-fasl manifest read through
  ;; here failed set-equality on Windows with every name carrying \r.
  (let ((n (string-length s)))
    (define (slice start end)
      (let ((end (if (and (> end start) (char=? (string-ref s (- end 1)) #\return))
                     (- end 1) end)))
        (substring s start end)))
    (let loop ((i 0) (start 0) (acc '()))
      (cond ((>= i n) (reverse (if (> i start) (cons (slice start i) acc) acc)))
            ((char=? (string-ref s i) #\newline)
             (loop (+ i 1) (+ i 1) (cons (slice start i) acc)))
            (else (loop (+ i 1) start acc))))))

(define (bld-file-lines path) (bld-string-lines (bld-source-string path)))

;; Build-time diagnostic. The runtime manifest carries one startup-profile mark
;; per ENTRY, so "host/chez/rt.ss" is a single 64ms line hiding the ~40 files it
;; transitively loads — enough to say the runtime is expensive, not enough to say
;; which part. JOLT_PROFILE_INLINE=1 at BUILD time emits a mark after each inlined
;; file, turning that one line into a per-file breakdown. Off by default: a
;; shipped binary carries the coarse set, and each mark costs a statistics call.
(define bld-profile-inline? (and (getenv "JOLT_PROFILE_INLINE") #t))

;; Emit one line to OUT, recursively inlining a `(load ...)` of a repo file.
(define (bld-inline-line line out depth)
  (when (> depth 50) (error 'jolt-build "load nesting too deep"))
  (let ((p (bld-load-path line)))
    (if p
        (begin
          (for-each (lambda (l) (bld-inline-line l out (+ depth 1))) (bld-file-lines p))
          (when bld-profile-inline?
            (bld-emit-startup-profile-mark! out (string-append "inlined " p))))
        (begin (put-string out line) (put-string out "\n")))))

;; Inline the runtime manifest, dispatching on the manifest tags. core-strs (the
;; shaken clojure.core defs, or #f) replaces the 'prelude blob; drop-compiler? (a
;; closed AOT app that never compiles from source) omits 'image + 'compile-eval —
;; the analyzer/back end are dead weight in the binary (~0.8MB).
(define (bld-emit-runtime out drop-compiler? core-strs)
  (bld-emit-startup-profile-preamble out)
  (bld-emit-startup-profile-mark! out "runtime begin")
  (for-each
    (lambda (entry)
      (let ((emitted?
              (cond
                ((eq? entry 'prelude)
                 (if core-strs
                     (begin
                       (for-each (lambda (s) (put-string out s) (put-string out "\n"))
                                 core-strs)
                       #t)
                     (begin
                       (bld-inline-line (cdr (assq 'prelude bld-tagged-loads)) out 0)
                       #t)))
                ((memq entry '(image compile-eval))
                 (if drop-compiler?
                     #f
                     (begin
                       (bld-inline-line (cdr (assq entry bld-tagged-loads)) out 0)
                       #t)))
                (else
                 (bld-inline-line entry out 0)
                 #t))))
        (when emitted?
          (bld-emit-startup-profile-mark!
            out
            (string-append "runtime " (bld-runtime-entry-label entry))))))
    bld-runtime-manifest))

;; --- app emission -----------------------------------------------------------
;; Re-emit one app namespace to a list of Scheme strings: run-passes (const-fold +
;; numeric-annotate in every mode; inference also in release/optimized; inline +
;; scalar-replace additionally with direct-link) and stay strict — a form that
;; fails to emit must fail the build, not vanish.
;; The loop itself is emit-image's ei-emit-ns* (optimize? #t, guard? #f).
(define (bld-emit-ns ns-name src) (ei-emit-ns* ns-name src #t #f))

;; --- whole-program inference pre-pass ---------------------------------------
;; Analyze every app form (all namespaces, deps-first) to IR and run the
;; closed-world param-type fixpoint, so each fn's param types pick up the record
;; types its callers pass. The per-ns emit below then bare-indexes field reads and
;; devirtualizes protocol calls at those sites (the back end reads the resulting
;; :hint/:devirt annotations). Optimized builds only; registries come from the
;; runtime tables populated as the app loaded.
(define jolt-wp-infer!             (var-deref "jolt.passes.types" "wp-infer!"))
(define jolt-wp-set-record-shapes! (var-deref "jolt.passes.types" "set-record-shapes!"))
(define jolt-wp-set-proto-methods! (var-deref "jolt.passes.types" "set-protocol-methods!"))
(define jolt-wp-host-record-shapes (var-deref "jolt.host" "record-shapes"))
(define jolt-wp-host-proto-methods (var-deref "jolt.host" "protocol-methods"))
(define jolt-contagion-prepass!      (var-deref "jolt.backend-scheme" "contagion-prepass!"))
(define jolt-contagion-prepass-done! (var-deref "jolt.backend-scheme" "contagion-prepass-done!"))
(define jolt-reset-clone-prepass!    (var-deref "jolt.backend-scheme" "reset-clone-prepass!"))

(define (bld-wp-infer! ordered)
  ;; the build's compilation unit (ei-unit) is created + published by the build setup
  ;; before any flag is set, so the whole-program seeds set here — and the mode flags —
  ;; land on the one unit the per-form emit reads.
  (jolt-wp-set-record-shapes! (ei-unit) (jolt-wp-host-record-shapes #f))
  (jolt-wp-set-proto-methods! (ei-unit) (jolt-wp-host-proto-methods #f))
  (let ((nodes '()) (ns-nodes '()))
    (for-each
      (lambda (nf)
        (set-chez-ns! (car nf))
        (let ((src (ei-timed "wp: read source" (lambda () (ldr-read-source (cdr nf))))) (per-ns '()))
          ;; This walk, not the emit walk, is where an --opt build's IR is
          ;; produced, so a file's top-level (set! *unchecked-math* …) has to be
          ;; in effect HERE for the analyzer to lower the following forms'
          ;; arithmetic to its wrapping variants. Bracketed per namespace, like
          ;; the loader does per file, so the flag doesn't leak into the next one.
          (dynamic-wind
            jolt-ns-load-vars-push!
            (lambda ()
           (parameterize ((rdr-source-file (cdr nf)))
             (jolt-enter-file! (cdr nf))   ; so a failure here names the file
             (let ((ord 0))
             (for-each
               (lambda (f)
                 ;; ord mirrors the loader's and the emit walk's per-file form
                 ;; counter (load-jolt-file* / ei-for-each-form): the
                 ;; def-ordinal visibility replay (rt.ss var-def-ordinals) must
                 ;; gate THIS analysis too — its cached IR is what the emit walk
                 ;; pops positionally, so a resolution decided here but not
                 ;; there (or vice versa) is the exact divergence the replay
                 ;; exists to prevent.
                 (parameterize ((jolt-form-ordinal ord))
                 (ce-scan-requires! f (car nf))
                 (when (ei-flag-set-form? f)
                   (jolt-compile-eval-form f (car nf)))
                 ;; per-ns is consumed POSITIONALLY by the emit walk
                 ;; (ei-next-cached, one pop per form ei-for-each-form
                 ;; dispatches). The emit walk compiles MACRO forms too, and
                 ;; keeps going past a form this analysis rejects — so both get
                 ;; a #f placeholder (ei-compile-form falls back to a fresh
                 ;; analysis on #f). Skipping them here shifted every later
                 ;; form's cached IR by one: a macro's def-var! captured the
                 ;; NEXT def's emission — invalid Scheme under direct-link, a
                 ;; silently corrupted expander before it. Only the ns form is
                 ;; skipped by BOTH walks.
                 (unless (ei-ns-form? f)
                   (if (ce-macro-form? f)
                       (set! per-ns (cons #f per-ns))
                       ;; a form the analyzer rejects here only loses
                       ;; whole-program type info (per-form emit still errors
                       ;; the build if it's truly broken) — but say so, or an
                       ;; optimized build silently loses inference for the ns.
                       (guard (e (#t (display (string-append
                                               "jolt build: note: whole-program inference skipped a form in "
                                               (car nf) "\n")
                                              (current-error-port))
                                     (set! per-ns (cons #f per-ns))))
                         (let ((n (ei-timed "wp: analyze"
                                    (lambda () (jolt-ce-analyze (make-analyze-ctx (car nf)) f)))))
                           (set! nodes (cons n nodes))
                           (set! per-ns (cons n per-ns))))))
                 (set! ord (fx+ ord 1))))
               (ei-timed "wp: parse" (lambda () (ei-read-all src)))))))
             jolt-ns-load-vars-pop!)
          (set! ns-nodes (cons (cons (car nf) (reverse per-ns)) ns-nodes))))
      ordered)
    (ei-timed "wp: fixpoint"
      (lambda () (jolt-wp-infer! (ei-unit) (apply jolt-vector (reverse nodes)))))
    ;; contagion clone-site pre-pass: an impl worth a specialized clone is one that is
    ;; BOTH contagion-eligible (:num field beside a proven :double) AND reached by a
    ;; devirtualized call site. Run per-ns after wp-infer! (rich field types must be
    ;; live) so a devirt site can resolve the clone regardless of emit order.
    (jolt-reset-clone-prepass! (ei-unit))
    ;; drop the #f alignment placeholders — the prepass wants real IR only.
    (ei-timed "wp: contagion prepass"
      (lambda ()
        (for-each (lambda (p) (jolt-contagion-prepass! (ei-unit)
                                (apply jolt-vector (filter (lambda (n) n) (cdr p))) (car p)))
                  ns-nodes)
        (jolt-contagion-prepass-done! (ei-unit))))
    (reverse ns-nodes)))

;; Strings emitted before each app ns's forms, replaying what the source loader
;; does per file: (1) set chez-current-ns so runtime ns-sensitive setup forms
;; (defmulti/defmethod resolve their target var through it) land in the right ns;
;; (2) register the ns's :as aliases so a quoted alias resolves at runtime — a
;; (defmethod ig/foo …) passes 'ig/foo to defmethod-setup, which needs ig -> the
;; real ns, but the build strips the (ns …) form that would register it.
(define (bld-scan-spec! ns-name spec emit!)
  (let ((items (cond ((pvec? spec) (seq->list spec))
                     ((cseq? spec) (seq->list spec))
                     (else '()))))
    (when (and (pair? items) (symbol-t? (car items)))
      (let ((target (symbol-t-name (car items))))
        (let loop ((xs (cdr items)))
          (when (and (pair? xs) (pair? (cdr xs)))
            (let ((k (car xs)) (v (cadr xs)))
              (when (keyword? k)
                (cond
                  ;; :as-alias registers the alias exactly like :as; what it does
                  ;; NOT do is pull the target into the build (bld-ns-requires).
                  ((and (or (string=? (keyword-t-name k) "as")
                            (string=? (keyword-t-name k) "as-alias"))
                        (symbol-t? v))
                   (emit! (string-append "(chez-register-alias! " (ei-str-lit ns-name)
                                         " " (ei-str-lit (symbol-t-name v))
                                         " " (ei-str-lit target) ")")))
                  ;; :refer [a b] / :refer :all — a defmethod on a referred multifn
                  ;; resolves the bare name through the refer table at runtime.
                  ((or (string=? (keyword-t-name k) "refer") (string=? (keyword-t-name k) "only"))
                   (cond
                     ((and (keyword? v) (string=? (keyword-t-name v) "all"))
                      (emit! (string-append "(chez-register-refer-all! " (ei-str-lit ns-name)
                                            " " (ei-str-lit target) ")")))
                     ((or (pvec? v) (cseq? v))
                      (for-each (lambda (n)
                                  (when (symbol-t? n)
                                    (emit! (string-append "(chez-register-refer! " (ei-str-lit ns-name)
                                                          " " (ei-str-lit (symbol-t-name n))
                                                          " " (ei-str-lit target) ")"))))
                                (seq->list v))))))))
            (loop (cddr xs))))))))

;; --- deferring the app's top-level forms out of the boot ---------------------
;; Chez does not schedule a forked thread until Sbuild_heap returns. The app's
;; emitted forms used to sit at the top level of the boot file, which is exactly
;; that window — so a namespace top-level form that spawned a thread and waited
;; for it never got its answer. Measured in a built binary: @(future …) hung
;; forever, an agent send + await-for never ran the action, a promise delivered
;; from a Thread timed out. All of them worked the moment -main started, and all
;; of them worked under `jolt -m`, where the namespace loads after boot. The
;; shape that found it was a top-level (clojure.java.shell/sh …): sh drains the
;; child through two futures and derefs them with NO timeout, so it hung the
;; process with no diagnostic.
;;
;; Proven below jolt: a boot file whose top level forks a thread and then sleeps
;; two seconds reports the child never ran, and the child runs only once
;; Sbuild_heap returns and Sscheme_start begins. Chez also refuses (collect) in
;; that window — "cannot collect when multiple threads are active" — so the
;; forked thread counts as active while being unable to run.
;;
;; So the app's forms move into the scheme-start launcher, which is past the
;; boundary. They cannot simply be wrapped in a lambda: the app emit produces
;; top-level (define jv$… …) forms interleaved with expressions, and Chez
;; rejects an internal definition after an expression in a body. Instead each
;; define is split — the binding is DECLARED at boot and ASSIGNED at init — so
;; every jv$ name still exists at the top level and cross-form references (which
;; is what direct-linking emits) are unchanged. Measured: an assigned top-level
;; variable costs nothing against an immutable one in a compile-file unit (479ms
;; vs 477ms on a 30M-iteration call loop), because such a unit compiles against
;; the interaction environment and so cannot assume immutability either way.

;; Index of PAT in S at or after START, or #f. Char-by-char rather than
;; substring: this runs over every emitted app form of a whole application.
(define (bld-find-substring s pat start)
  (let ((n (string-length s)) (m (string-length pat)))
    (let loop ((i start))
      (cond ((fx> (fx+ i m) n) #f)
            ((let cmp ((j 0))
               (or (fx= j m)
                   (and (char=? (string-ref s (fx+ i j)) (string-ref pat j))
                        (cmp (fx+ j 1)))))
             i)
            (else (loop (fx+ i 1)))))))

(define (bld-prefix? s pre)
  (let ((n (string-length s)) (m (string-length pre)))
    (and (fx>= n m) (string=? (substring s 0 m) pre))))

;; The top-level (define nm …) names in one emitted app form, walking the
;; (begin …) splice the def emit wraps its registrations in.
;;
;; Anything else that binds at the top level is refused rather than passed
;; through: a define-record-type or a procedure-style define needs restructuring,
;; not a set!, and would otherwise land in the init body as an illegal internal
;; definition — or worse, compile and shadow. Today's app emit produces neither
;; (records, protocols and deftypes all lower to (define jv$… <init>) plus
;; runtime registration calls), so this is a tripwire on that staying true.
(define (bld-app-form-defines s)
  (let ((names '()))
    (let ((ip (open-input-string s)))
      (let loop ((f (read ip)))
        (unless (eof-object? f)
          (let walk ((f f))
            (when (and (pair? f) (symbol? (car f)))
              (cond
                ((eq? (car f) 'begin) (for-each walk (cdr f)))
                ((eq? (car f) 'define)
                 (let ((h (and (pair? (cdr f)) (cadr f))))
                   (unless (symbol? h)
                     (error 'bld-app-form-defines
                            "app form has a procedure-style define; cannot defer it" h))
                   (set! names (cons h names))))
                ((bld-prefix? (symbol->string (car f)) "define")
                 (error 'bld-app-form-defines
                        "app form has a top-level binding form the deferral cannot split"
                        (car f)))
                (else #f))))
          (loop (read ip)))))
    (reverse names)))

;; Split APP-STRS into the declarations that stay at boot and the bodies that run
;; from the launcher. Each (define nm <init>) becomes a bare (define nm) up top
;; and a (set! nm <init>) in the body, rewritten textually so the emitted source
;; is otherwise byte-identical — the line-number comments the back end threads
;; through it survive, and no read/write round trip can perturb a literal. The
;; jv$ name is unique to its var, so the occurrence is unambiguous; exactly one
;; is required, and a miss fails the build rather than silently leaving a define
;; that would become an illegal internal definition.
(define (bld-defer-app-strs app-strs)
  (let loop ((rest app-strs) (decls '()) (bodies '()))
    (if (null? rest)
        (values (reverse decls) (reverse bodies))
        (let* ((s (car rest))
               (names (bld-app-form-defines s)))
          (loop (cdr rest)
                (fold-left (lambda (acc nm)
                             (cons (string-append "(define " (symbol->string nm) " (void))") acc))
                           decls names)
                (cons (fold-left
                        (lambda (str nm)
                          (let* ((n (symbol->string nm))
                                 (pat (string-append "(define " n " "))
                                 (at (bld-find-substring str pat 0)))
                            (unless at
                              (error 'bld-defer-app-strs "no (define …) text for" nm))
                            (when (bld-find-substring str pat (fx+ at 1))
                              (error 'bld-defer-app-strs "ambiguous (define …) text for" nm))
                            (string-append (substring str 0 at)
                                           "(set! " n " "
                                           (substring str (fx+ at (string-length pat))
                                                      (string-length str)))))
                        s names)
                      bodies)))))) 

;; The init bodies as procedures the launcher calls, in order. Chunked rather
;; than one procedure: a whole application's forms in a single lambda body is one
;; enormous letrec* for Chez to compile, where the boot file used to hand it many
;; small top-level forms. An empty chunk is not emitted — a lambda needs a body.
(define bld-app-init-chunk 100)
(define (bld-emit-app-init out bodies)
  (let loop ((rest bodies) (k 0) (names '()))
    (if (null? rest)
        (begin
          (put-string out "(define (jolt-app-init!)\n")
          (if (null? names)
              (put-string out "  #f")
              (for-each (lambda (n) (put-string out (string-append "  (" n ")\n")))
                        (reverse names)))
          (put-string out ")\n"))
        (let* ((nm (string-append "jolt-app-init$" (number->string k) "!"))
               (chunk (let take ((r rest) (i 0) (acc '()))
                        (if (or (null? r) (fx= i bld-app-init-chunk))
                            (reverse acc)
                            (take (cdr r) (fx+ i 1) (cons (car r) acc)))))
               (after (let drop ((r rest) (i 0))
                        (if (or (null? r) (fx= i bld-app-init-chunk)) r (drop (cdr r) (fx+ i 1))))))
          (put-string out (string-append "(define (" nm ")\n"))
          (for-each (lambda (s) (put-string out s) (put-string out "\n")) chunk)
          (put-string out ")\n")
          (loop after (fx+ k 1) (cons nm names))))))

(define (bld-ns-prelude ns-name src)
  (let ((acc (list (string-append "(set-chez-ns! " (ei-str-lit ns-name) ")")))
        (nsf (let loop ((fs (ei-read-all src)))
               (cond ((null? fs) #f)
                     ((ei-ns-form? (car fs)) (car fs))
                     (else (loop (cdr fs)))))))
    (when nsf
      (for-each
        (lambda (clause)
          (when (cseq? clause)
            (let ((citems (seq->list clause)))
              (when (and (pair? citems) (keyword? (car citems))
                         (let ((kn (keyword-t-name (car citems))))
                           (or (string=? kn "require") (string=? kn "use"))))
                (for-each (lambda (spec)
                            (bld-scan-spec! ns-name spec
                                            (lambda (s) (set! acc (cons s acc)))))
                          (cdr citems)))
              ;; :import must be reconstructed too: ei-for-each-form skips the
              ;; ns form entirely, so without this a built binary never runs
              ;; __import and the class-valued short-name vars (Path,
              ;; FileAttribute, …) stay unbound — late-bind used to mask that.
              (when (and (pair? citems) (keyword? (car citems))
                         (string=? (keyword-t-name (car citems)) "import"))
                (for-each
                  (lambda (spec)
                    (set! acc (cons (string-append
                                      "(chez-runtime-import (jolt-read-string "
                                      (ei-str-lit (jolt-pr-str spec)) "))")
                                    acc)))
                  (cdr citems))))))
        (seq->list nsf)))
    (reverse acc)))

;; --- AOT the CLI entry closure (jolt.main + jolt.deps) -----------------------
;; jolt.main + jolt.deps and their on-demand Clojure require closure (clojure.string,
;; clojure.edn, jolt.mvn-http, jolt.ffi, grenadine.*) must NOT be baked as top-level
;; (load-namespace …) forms in flat.ss: flat.so is a Chez boot file whose top-level
;; forms re-execute at every Sbuild_heap (every process start), so those load-namespace
;; calls re-analyze and re-emit the whole graph from Clojure source on EVERY invocation
;; — the measured ~380ms release floor, ~1.3s in the dev boot cache. Instead we emit
;; their Scheme HERE, at image-emit time, via the same emit-image path an app build
;; uses, so at boot the vars are defined by running compiled Scheme (a few ms) exactly
;; like the rest of the runtime image.
;;
;; The runtime + compiler image + clojure.core are already emitted (bld-emit-runtime,
;; which the caller ran before this). We load jolt.main + jolt.deps in THIS build
;; process to populate the compiler's registries, capturing the on-demand load order
;; via the loader's ns-loaded-hook — which fires only for namespaces NOT already in
;; the image, i.e. exactly the CLI's on-demand closure. Each ns is emitted var-routed
;; (prelude mode, direct-link OFF) so its runtime behavior is identical to the
;; interpreted load it replaces. A form that fails to emit fails the build
;; (bld-emit-ns is strict), same as an app build. jolt.deps's lazy in-fn
;; (require 'clojure.data.json) is not on the load path here, so it stays
;; load-on-demand at runtime — unchanged. Used by BOTH the release build
;; (build-jolt.ss) and the dev boot cache (make-devboot.ss): one artifact shape.
(define (bld-emit-cli-aot out)
  (put-string out "\n;; === AOT jolt.main + jolt.deps (emitted Scheme) ===\n")
  (let ((order '()))
    (set-ns-loaded-hook! (lambda (name file) (set! order (cons (cons name file) order))))
    (parameterize ((ldr-source-only? #t))    ; emit from source, never a compiled artifact
      (load-namespace "jolt.main")
      (load-namespace "jolt.deps"))
    (set-ns-loaded-hook! (lambda (name file) #f))
    (let ((ordered (reverse order)))   ; deps complete loading before requirers -> deps-first
      (when (null? ordered)
        (error 'bld-emit-cli-aot "no CLI namespace captured for jolt.main — is jolt-core on the source roots?"))
      (dynamic-wind
        (lambda ()
          (ei-fresh-unit!)
          ((var-deref "jolt.backend-scheme" "set-prelude-mode!") #t)
          (set-optimize! #t)
          ((var-deref "jolt.backend-scheme" "set-var-cache!") #t))
        (lambda ()
          (for-each
            (lambda (nf)
              (let ((name (car nf)) (src (ldr-read-source (cdr nf))))
                (put-string out (string-append "\n;; --- AOT " name " ---\n"))
                (parameterize ((rdr-source-file (cdr nf)))
                  (put-string out "(jolt-ns-load-vars-push!)\n")
                  (for-each (lambda (s) (put-string out s) (put-string out "\n"))
                            (bld-ns-prelude name src))
                  (for-each (lambda (s) (put-string out s) (put-string out "\n"))
                            (bld-emit-ns name src))
                  (put-string out "(jolt-ns-load-vars-pop!)\n")
                  ;; Record the ns as loaded so the runtime dispatch's
                  ;; (load-namespace "jolt.main") in cli-core.ss is a no-op — the
                  ;; defines above already installed every var. Without this the
                  ;; loader sees an unmarked ns and recompiles it from source on the
                  ;; first command that enters jolt.main/-main (run/build/version).
                  (put-string out (string-append "(ldr-mark-loaded! " (ei-str-lit name) ")\n"))
                  ;; ...and record that this ns is preloaded only because the CLI
                  ;; image defines it, so an app build does not inherit the claim.
                  (put-string out (string-append "(ldr-mark-cli-aot! " (ei-str-lit name) ")\n")))))
            ordered))
        (lambda ()
          (set-optimize! #f)
          ((var-deref "jolt.backend-scheme" "set-var-cache!") #f)
          (ei-clear-cached!))))))

;; --- bundling: native libs + resources --------------------------------------
;; A jolt seq of jolt strings -> a Scheme list of Scheme strings.
(define (bld-strs x) (map jolt-str-render-one (seq->list x)))

;; Emit native-library loads. `natives` is the encoded jolt seq jolt.main/
;; encode-natives produced: each entry is ["process"] | ["static" form…] |
;; ["req" cand…] | ["opt" cand…]. `which` selects 'required (process + static +
;; req) or 'optional. Required loads are emitted before the app forms (the app's
;; defcfn foreign-procedures are now lazily resolved on first call, so they can
;; be emitted before the library is loaded — the binding only becomes callable
;; after the lib loads); a load-shared-object failure there is fatal — correct
;; for a required lib. A "static" lib is cc-linked into the binary (see
;; bld-native-link-flags), so its symbols are already in the process: it loads
;; them the same way a "process" lib does. Optional loads run in the scheme-start
;; launcher, where guard catches a missing lib (the defcfn's foreign-procedure is
;; only resolved when the closure is first called, so the defining form can
;; evaluate before the library is loaded).
(define (bld-emit-natives out natives which)
  (for-each
    (lambda (entry)
      (let* ((parts (bld-strs entry)) (kind (car parts)) (cands (cdr parts))
             (cand-lits (fold-left (lambda (s c) (string-append s (ei-str-lit c) " ")) "" cands)))
        (cond
          ((and (eq? which 'required) (or (string=? kind "process") (string=? kind "static")))
           (put-string out "(jolt-build-load-native '() #f #t)\n"))
          ((and (eq? which 'required) (string=? kind "req"))
           (put-string out (string-append "(jolt-build-load-native (list " cand-lits ") #f #f)\n")))
          ((and (eq? which 'optional) (string=? kind "opt"))
           (put-string out (string-append "(jolt-build-load-native (list " cand-lits ") #t #f)\n"))))))
    (seq->list natives)))

;; The cc link fragment for the "static" natives: each archive must be FORCE-loaded
;; (the linker would otherwise drop an archive member main.c never references) and,
;; on Linux, the executable's symbols exported into the dynamic table so the
;; startup (load-shared-object #f) + foreign-procedure can resolve them (-rdynamic,
;; added by build-with-cc when this fragment is non-empty). Returns "" when no lib
;; is statically linked. Entry forms: ["static" "archive" path] | ["static" "lib"
;; name libdir].
(define (bld-native-link-flags natives)
  (fold-left
    (lambda (acc entry)
      (let ((parts (bld-strs entry)))
        (if (string=? (car parts) "static")
            (string-append acc " " (bld-one-static-link (cdr parts)))
            acc)))
    "" (seq->list natives)))

;; A statically-linked native is only in the OUTPUT binary, but build step 1
;; evaluates the app's `foreign-procedure` forms in THIS process (to register its
;; macros/vars), and Chez resolves a foreign entry eagerly. So make the archive's
;; symbols resolvable here: build a throwaway shared object from it (force-loading
;; every member) and load it. The output binary still cc-links the static archive;
;; this temp .so is build-time only. Only the "archive" form is preloaded — the
;; "lib" form names a system library the OS loader already finds by soname.
(define (bld-preload-static-natives! natives builddir)
  (let ((n 0))
    (for-each
      (lambda (entry)
        (let ((parts (bld-strs entry)))
          (when (and (string=? (car parts) "static") (string=? (cadr parts) "archive"))
            (let* ((archive (caddr parts))
                   (so (string-append builddir "/native-" (number->string n)
                                      (if bld-osx? ".dylib" ".so"))))
              (set! n (+ n 1))
              (bld-system
                (if bld-osx?
                    (string-append "cc -dynamiclib -undefined dynamic_lookup -Wl,-all_load '"
                                   archive "' -o '" so "'")
                    (string-append "cc -shared -Wl,--whole-archive '" archive
                                   "' -Wl,--no-whole-archive -Wl,--unresolved-symbols=ignore-all -o '" so "'")))
              (sa-load-shared-object so)))))
      (seq->list natives))))

(define (bld-one-static-link form)
  (let ((kind (car form)))
    (cond
      ((string=? kind "archive")
       (let ((path (cadr form)))
         (if bld-osx?
             (string-append "-Wl,-force_load," (bld-sh-quote path))
             (string-append "-Wl,--whole-archive " (bld-sh-quote path) " -Wl,--no-whole-archive"))))
      ((string=? kind "lib")
       (let* ((lib (cadr form)) (dir (caddr form))
              (L (if (> (string-length dir) 0) (string-append "-L" dir " ") "")))
         ;; -Bstatic forces the .a over a .so of the same -l name (GNU ld). macOS's
         ;; ld64 has no -Bstatic; there an :archive path is the reliable form.
         (if bld-osx?
             (string-append (if (> (string-length dir) 0) (string-append "-L" (bld-sh-quote dir) " ") "") "-l" lib)
             (string-append (if (> (string-length dir) 0) (string-append "-L" (bld-sh-quote dir) " ") "") "-Wl,-Bstatic -l" lib " -Wl,-Bdynamic"))))
      (else ""))))

;; Walk an embed root recursively; return (resource-name . abspath) pairs, where
;; resource-name is the "/"-joined path under the root (what io/resource is asked for).
(define (bld-walk-files root rel acc)
  (let ((dir (if (string=? rel "") root (string-append root "/" rel))))
    (fold-left
      (lambda (acc name)
        (let* ((relpath (if (string=? rel "") name (string-append rel "/" name)))
               (full (string-append root "/" relpath)))
          (if (file-directory? full)
              (bld-walk-files root relpath acc)
              (cons (cons relpath full) acc))))
      acc
      (directory-list dir))))

;; Emit register-embedded-resource! per file under each embed dir. Emitted BEFORE
;; the app forms. File contents are read at BUILD time and emitted as bytevector
;; literals (1B/char) — flat.ss top-level forms run at every startup with no source
;; on disk, so read-file-string at runtime would fail.
(define (bld-emit-embeds out embed-dirs)
  (for-each
    (lambda (root)
      (when (file-directory? root)
        (for-each
          (lambda (rp)
            (put-string out (string-append
                              "(register-embedded-resource! " (ei-str-lit (car rp))
                              " " (ei-bytes-lit (read-file-string (cdr rp))) ")\n")))
          (bld-walk-files root "" '()))))
    (bld-strs embed-dirs)))

;; Namespaces defined in the runtime image before the CLI loads jolt.main and
;; its require closure. By the time build-binary is called, jolt.main has loaded
;; jolt.ffi and other lazy stdlib namespaces into THIS process, but those are not
;; in the app image being written. Re-snapshotting loaded-ns there silently
;; leaves their vars interned but UNBOUND in a source-mode-built app/library.
;;
;; The baseline is taken in loader.ss, and bld-runtime-manifest below loads
;; loader.ss last but for java/ffi.ss (which defines no namespace of its own) —
;; so the baseline is exactly the set an app image inherits. A load added after
;; loader.ss there makes the baseline too SMALL, an over-emit that costs bytes;
;; too large is the direction that leaves vars unbound.
(define bld-boot-loaded (ldr-runtime-image-ns-copy))

;; --- the build --------------------------------------------------------------
;; entry-ns: the app's main namespace (a string). out-path: the binary to write.
;; mode: "dev" | "release" | "optimized". Every form runs through jolt.passes/
;; run-passes (const-fold always; type inference in every mode but dev; inline +
;; scalar-replace additionally when direct-linked). Deps + source roots are already
;; applied by the caller.
;; natives: encoded :jolt/native libs to load at startup. embed-dirs: dirs whose
;; files bake into the binary (single-file). ext-roots: project-relative io/resource
;; roots resolved at runtime against JOLT_PWD (ship-alongside resources).
;; allow-dynamic: "ns/name" strings the project and its deps vouch never resolve
;; vars at runtime in the built binary (deps.edn :jolt/tree-shake {:allow-dynamic
;; […]}); dce-shake skips them in its bail and compiler-needed scans (see
;; dce-bail-scan). '() when nothing declared one.
;; direct-link?: closed-world direct-linking (app->app calls bind directly; a plain
;; def is frozen, ^:redef/^:dynamic stay var-routed). The caller (jolt.main) turns
;; this ON for release and optimized and OFF for --dev / --no-direct-link.
(define (bld-suffix? s suf)
  (let ((n (string-length s)) (m (string-length suf)))
    (and (>= n m) (string=? (substring s (- n m) n) suf))))
;; --- derive namespace roots from the require graph ---------------------------
;; Data-reader namespaces load during project setup, before build-binary arms its
;; ns-loaded hook, so the entry walk records nothing for them. Collect their ns
;; names (symbol ns parts) from *data-readers*; build-binary runs the require
;; closure over them to pull in their transitive deps too.
(define (bld-data-reader-ns-names)
  (let ((tbl (var-deref "clojure.core" "*data-readers*")) (acc '()))
    (when (pmap? tbl)
      (pmap-fold tbl
        (lambda (k v a)
          (when (and (symbol-t? v) (symbol-t-ns v) (not (jolt-nil? (symbol-t-ns v))))
            (let ((nm (symbol-t-ns v)))
              (unless (member nm acc)
                (set! acc (cons nm acc)))))
          a)
        #f))
    (reverse acc)))

;; Walk top-level forms in a source file and return the list of namespace name
;; STRINGS that this file requires (via ns :require/:use clauses and top-level
;; require/use forms). Only top-level forms are inspected — no recursion into
;; subforms (the quoted-data bug ce-scan-requires! has). Specs are parsed through
;; the shared expand-spec + parse-libspec (loader.ss / ns.ss), matching the
;; loader's semantics exactly.
;; A libspec that only establishes an alias pulls nothing into the build. At
;; runtime `require` interns the namespace without loading it (ns.ss
;; ns-load+register), so counting it as a dependency would emit the target into
;; the binary and run its top level — the opposite of what :as-alias asks for. The
;; alias itself is still replayed, by bld-scan-spec!. Mirrors clojure.core's
;; load-lib, which picks its loader with `need-ns (or as use)`.
(define (bld-spec-alias-only? parsed use?)
  (let ((opt-names (map car (cdr parsed))))
    (and (member "as-alias" opt-names)
         (not (member "as" opt-names))
         (not use?))))

(define (bld-ns-requires file)
  (let ((src (ldr-read-source file)) (reqs '()))
    (for-each
      (lambda (form)
        (when (cseq? form)
          (let* ((items (seq->list form))
                 (h (and (pair? items) (car items)))
                 (hn (and (symbol-t? h) (symbol-t-name h))))
            (cond
              ;; (ns name (:require spec...) ...)
              ((and hn (string=? hn "ns"))
               (for-each
                 (lambda (clause)
                   (when (cseq? clause)
                     (let ((cl (seq->list clause)))
                       (when (and (pair? cl) (keyword? (car cl))
                                  (let ((kn (keyword-t-name (car cl))))
                                    (or (string=? kn "require") (string=? kn "use"))))
                         (for-each
                           (lambda (spec)
                             (for-each
                               (lambda (s)
                                 (let ((parsed (parse-libspec s)))
                                   (when (and parsed
                                              (not (bld-spec-alias-only?
                                                     parsed
                                                     (string=? (keyword-t-name (car cl)) "use"))))
                                     (set! reqs (cons (car parsed) reqs)))))
                               (expand-spec spec)))
                           (cdr cl))))))
                 (if (pair? (cdr items)) (cddr items) '())))
              ;; (require spec...) / (use spec...) — specs are quoted
              ((and hn (or (string=? hn "require") (string=? hn "use")))
               (for-each
                 (lambda (a)
                   (let ((unquoted (ce-unquote a)))
                     (for-each
                       (lambda (s)
                         (let ((parsed (parse-libspec s)))
                           (when (and parsed
                                      (not (bld-spec-alias-only? parsed (string=? hn "use"))))
                             (set! reqs (cons (car parsed) reqs)))))
                       (expand-spec unquoted))))
                  (cdr items)))))))
      ;; scan mode: this read happens BEFORE any namespace is loaded, so
      ;; alias-resolved auto keywords (::alias/kw) can't resolve yet — read
      ;; them leniently; only require clauses are extracted from these forms.
      ;; rdr-source-file scopes the file the way the inference and emit walks below
      ;; already do, so a reader error carries it in the message; jolt-enter-file!
      ;; records it for the uncaught reporter, which runs after every dynamic
      ;; binding here has unwound. This walk evaluates nothing, so the two of them
      ;; are the only record of which file a failure came from.
      (parameterize ((rdr-scan-mode #t) (rdr-source-file file))
        (jolt-enter-file! file)
        (map rdr-form->data (ei-read-all src))))
    (reverse reqs)))

;; Host classes a file's forms reference that a PROVIDER installs (RFC 0014). At
;; runtime a class-miss autoloads the provider's install namespace off the source
;; roots (host-static.ss lib-try-autoload!); a built binary has no source roots,
;; so the scan must pull the provider into flat.ss instead — otherwise the binary
;; throws the "add it to your deps.edn" error for a dependency the project did
;; declare.
;;
;; This asks lib-provider-for, the same table the runtime resolves against,
;; rather than restating its rules. The previous version mirrored the runtime
;; predicates by hand and had to be kept in step with them.
;;
;; A provider is pulled only when its source is actually on the roots
;; (find-ns-file) — off the roots the runtime's unknown-class message is the
;; contract and the build must keep succeeding exactly as before.
(define (bld-ns-class-providers file)
  (let ((src (ldr-read-source file))
        (cands '()))
    (define (add! class)
      (let ((cand (cond ((lib-provider-for class) => (lambda (p) (vector-ref p 0)))
                        (else #f))))
        (when (and cand (not (member cand cands)))
          (set! cands (cons cand cands)))))
    (define (walk x)
      (cond ((symbol-t? x)
             (let ((ns (symbol-t-ns x)))
               ;; two reference shapes: java.util.Locale/US (the ns segment IS the
               ;; class) and a bare java.time.ZonedDateTime (no slash — the whole
               ;; name is the class, namespace is nil).
               (if (and ns (not (jolt-nil? ns)))
                   (add! ns)
                   (add! (symbol-t-name x)))))
            ((cseq? x) (for-each walk (seq->list x)))
            ((pvec? x) (for-each walk (seq->list x)))
            ((pmap? x) (pmap-fold x (lambda (k v a) (walk k) (walk v) #f) #f))))
    (parameterize ((rdr-scan-mode #t) (rdr-source-file file))
      (jolt-enter-file! file)
      (for-each (lambda (f) (walk (rdr-form->data f))) (ei-read-all src)))
    (filter (lambda (c) (find-ns-file c)) cands)))

;; Post-order DFS from a list of root namespace names: for each name, find its
;; file, recurse into its requires, then append (name . file). Already-visited
;; names are skipped (cycles terminate). Names whose source file can't be found
;; (AOT/in-memory) are skipped — they resolve elsewhere.
;; IMPORTANT: namespaces whose resolved file is jolt-runtime-owned (embedded
;; resource or under ldr-install-roots) are skipped ONLY when already defined
;; at boot (bld-boot-loaded, the seed image's set) — they are preloaded at jolt
;; boot, and emitting them into the app section would bloat the binary and
;; break direct-link bindings. LAZY stdlib (in the install roots but NOT in
;; the seed — e.g. jolt.time.impl) IS included and emitted: a built binary
;; has no disk roots, so its compiled Scheme must define those vars at boot —
;; the same reason bld-emit-cli-aot emits jolt.main into the release image.
;; ldr-cli-aot? is the same claim reached from the other side: a release CLI
;; bakes jolt.main's closure into its OWN heap, and bld-boot-loaded is taken
;; before that (loader.ss), so the two agree — it stays as the explicit record
;; of WHY such a namespace is preloaded, and covers any image that ever seeds
;; the CLI closure earlier.
;; Result: deps first, roots last.
(define (bld-require-closure names)
  (let ((visited (make-hashtable string-hash string=?))
        (order '()))
    (let dfs ((ns names))
      (unless (null? ns)
        (let ((name (car ns)))
          (unless (hashtable-ref visited name #f)
            (hashtable-set! visited name #t)
            (let ((file (find-ns-file name)))
              (when (and file
                         (or (not (ldr-install-file? file))
                             (not (hashtable-ref bld-boot-loaded name #f))
                             ;; preloaded only in the CLI image, not in an app's
                             (ldr-cli-aot? name)))
                (dfs (append (bld-ns-class-providers file) (bld-ns-requires file)))
                (set! order (cons (cons name file) order)))))
          (dfs (cdr ns)))))
    (reverse order)))

;; Bake the *data-readers* table into the binary so a runtime (read-string
;; "#my/tag …") resolves its reader fn like it does under jolt run. A reader is
;; written as the SYMBOL naming its var (a var value is written as its own name);
;; the reader path var-derefs the fn at use time.
(define (bld-sym-lit s)
  (let ((ns (symbol-t-ns s)))
    (if (and ns (not (jolt-nil? ns)))
        (string-append "(jolt-symbol " (ei-str-lit ns) " " (ei-str-lit (symbol-t-name s)) ")")
        (string-append "(jolt-symbol #f " (ei-str-lit (symbol-t-name s)) ")"))))
;; A table entry's source text, or #f for one that cannot be written as a literal.
;; A FUNCTION value — the shape (alter-var-root #'*data-readers* assoc 'my/tag
;; (fn …)) leaves — is skipped rather than emitted: a closure has no literal form,
;; and the top-level code that installed it is in the binary and re-runs at
;; startup, so the entry is back in the table before anything reads a #tag.
;; Without the skip the emit walked a procedure into bld-sym-lit and the build
;; died in symbol-t-ns.
(define (bld-data-reader-lit v)
  (cond ((symbol-t? v) (bld-sym-lit v))
        ((var-cell? v) (bld-sym-lit (jolt-symbol (var-cell-ns v) (var-cell-name v))))
        (else #f)))
(define (bld-emit-data-readers out)
  (let ((tbl (var-deref "clojure.core" "*data-readers*")))
    (when (pmap? tbl)
      (let ((pairs (pmap-fold tbl
                     (lambda (k v a)
                       (let ((klit (and (symbol-t? k) (bld-sym-lit k)))
                             (vlit (bld-data-reader-lit v)))
                         (if (and klit vlit) (cons (cons klit vlit) a) a)))
                     '())))
        (when (pair? pairs)
          (put-string out "\n;; === data readers ===\n")
          (put-string out "(def-var! \"clojure.core\" \"*data-readers*\"\n  (jolt-assoc empty-pmap")
          (for-each (lambda (p) (put-string out (string-append "\n    " (car p) " " (cdr p)))) pairs)
          (put-string out "))\n"))))))

(define (build-binary entry-ns out-path mode natives embed-dirs ext-roots direct-link? tree-shake? allow-dynamic library?)
  (ei-profile-init!)
  ;; Windows executables carry .exe; normalize here so the append-payload and
  ;; cc paths agree and the shell can run the result. A library keeps its own
  ;; suffix (.dll/.so/.dylib) — never rewrite it to .exe.
  (let ((out-path (if (and (bld-tgt-nt?) (not library?) (not (bld-suffix? out-path ".exe")))
                      (string-append out-path ".exe")
                      out-path)))
  ;; The self-contained path (jolt-embedded-bytes "stub/launcher") needs no csv
  ;; kernel files, no Chez, no cc — only the legacy cc path does. A --library build
  ;; always takes build-shared, and any cross build takes a spawned cc path, so both
  ;; need the toolchain even from the self-contained jolt.
  (when (or library? (bld-cross?) (not (jolt-embedded-bytes "stub/launcher"))) (bld-check-toolchain))
  ;; Static natives have to be loaded into this HOST process while the app is
  ;; emitted, so a target-architecture archive cannot be supported merely by
  ;; handing it to the target linker. Refuse before bld-preload-static-natives!
  ;; tries to turn one into a host shared object.
  (when (and (bld-cross?) (> (string-length (bld-native-link-flags natives)) 0))
    (error 'jolt-build
      "cross build (--target) does not support :jolt/native archives yet (they need separate host and target archives)"))
  (when (> (string-length (bld-native-link-flags natives)) 0)
    ;; :static natives are cc-linked into the binary, so a C compiler must be on
    ;; PATH — the self-contained jolt bundles the Chez kernel (libkernel.a +
    ;; scheme.h) and relinks a custom stub (see build-self-contained), but still
    ;; needs the system cc for that link. Fail early (before the app's foreign-
    ;; procedure forms eval below) with an actionable message.
    (unless (bld-have-cc?)
      (error 'jolt-build
        "static native linking needs a C compiler (cc) on PATH; install one, or pass --dynamic to load the library at runtime."))
    ;; Preload static archives' symbols into this process so step 1's foreign-
    ;; procedure evals resolve; the .build dir must exist first.
    (bld-mkdir-p (string-append out-path ".build"))
    (bld-preload-static-natives! natives (string-append out-path ".build")))
   ;; 1. record app namespaces in dependency order as they finish loading.
   (let ((app-order '()))
     (set-ns-loaded-hook!
      (lambda (name file) (set! app-order (cons (cons name file) app-order))))
    (ei-mark! "startup")
    (parameterize ((ldr-source-only? #t))    ; emit from source, never a compiled artifact
      (load-namespace entry-ns))
    (ei-mark! "load app from source")
    ;; Build ordered ns list from the require graph (static scan of source files)
    ;; merged with the hook's load order. The graph gives post-order deps; the
    ;; hook captures dynamic requires the static scan can't see.
    (let* ((graph (bld-require-closure (list entry-ns)))
           (_prof-graph (ei-mark! "require-graph DFS"))
           ;; reader namespaces with transitive closure
           (reader-ns-names (bld-data-reader-ns-names))
           (reader-pairs (bld-require-closure reader-ns-names))
           ;; Namespaces the CLASS scan pulled in (lib providers like jolt.time)
           ;; and the data-reader namespaces are in the closure without ever
           ;; having been loaded in-process: step 1 only loads what the entry's
           ;; requires reach, and the runtime class-miss autoload fires on USE,
           ;; which the build never triggers. Load them now, source-only like
           ;; step 1 and with the hook STILL RECORDING. The strict emit below
           ;; re-analyzes every source against process ns state, and an unloaded
           ;; provider has no refer/alias tables, so its own :refer'd names would
           ;; not resolve — that is why they load at all. They load under the hook
           ;; because the emit ORDER has to be the order the loader ran them in: a
           ;; provider's install namespace can depend on a namespace step 1
           ;; already loaded through another path, and only the hook sees that
           ;; edge. jolt.time's formatter half (a git dep) calls
           ;; jolt.time.impl/register-type! at its top level, and impl — the
           ;; embedded-stdlib half of the same provider — was loaded in step 1 when
           ;; the entry's def touched LocalDateTime. Placing the never-loaded set
           ;; in front of walked by the static graph alone put jolt.time.fmt before
           ;; jolt.time.impl, and the binary died at startup on
           ;; (impl/register-type! …) with "Attempting to call unbound fn" (#944).
           (_loaded (begin
                      (for-each
                        (lambda (p)
                          (unless (hashtable-ref loaded-ns (car p) #f)
                            (parameterize ((ldr-source-only? #t))
                              (load-namespace (car p)))))
                        (append graph reader-pairs))
                      (set-ns-loaded-hook! (lambda (name file) #f))
                      #t))
           (walked (reverse app-order))
           ;; graph without the entry-ns pair (it goes last)
           (graph-rest (if (and (pair? graph)
                                (string=? (caar (reverse graph)) entry-ns))
                           (reverse (cdr (reverse graph)))
                           graph))
           ;; only keep reader pairs not already in graph-rest or walked
           (reader-pairs
             (filter (lambda (p)
                       (not (or (assoc (car p) graph-rest)
                                (assoc (car p) walked))))
                     reader-pairs))
           ;; merge: reader pairs, then the static-graph namespaces the hook never
           ;; saw, then the hook's own order.
           ;;
           ;; walked is authoritative. The hook fires AFTER a namespace finishes
           ;; loading, so every dependency is already in the list — including the
           ;; ones bld-require-closure drops for being install-owned (jolt's own
           ;; stdlib: jolt/time/impl.clj, util.clj, …). Appending walked LAST put
           ;; those behind the library namespaces whose top level calls them, so a
           ;; built binary died at startup on (impl/register-type! …) with
           ;; "Attempting to call unbound fn".
           ;;
           ;; With the closure loaded above, a graph-rest entry missing from walked
           ;; was already loaded before step 1 (dep resolution, boot), and so was
           ;; everything it requires — nothing in walked can be its dependency.
           ;; Those go in front, keeping bld-require-closure's post-order among
           ;; themselves.
           (pre (remp (lambda (p) (assoc (car p) walked)) graph-rest))
           (merged (append reader-pairs pre walked))
           ;; ensure entry-ns is last
           (entry-pair (or (assoc entry-ns merged)
                           (assoc entry-ns walked)
                           (cons entry-ns (find-ns-file entry-ns))))
           (ordered (append (remp (lambda (p) (string=? (car p) entry-ns)) merged)
                            (list entry-pair))))
       (when (null? ordered)
         (error 'jolt-build (string-append "no source namespace loaded for " entry-ns
                                           " — is it on the source roots?")))
      ;; 2. emit each app namespace. Every mode but dev runs the inference +
      ;; record-shape setup passes and the inline + flatten + scalar-replace
      ;; fixpoint (set-optimize! below; inlining follows direct-link); release
      ;; and optimized differ only in the Chez compile parameters. Dev mode
      ;; gets const-fold + numeric-annotate only.
      ;; direct-link? commits to a closed world: app->app calls bind directly, a
      ;; plain def is frozen in the binary (^:redef/^:dynamic stay var-routed).
      ;; The caller (jolt.main) turns it ON for release and optimized and OFF for
      ;; --dev / --no-direct-link. The defined-set accumulates across the
      ;; dependency-ordered namespaces, so a dep's defs are direct-linkable by the
      ;; time the entry that calls them is emitted.
      ;; set-optimize!/set-direct-link! are process-global flags in the back end;
      ;; dynamic-wind guarantees they revert even if a strict form errors mid-emit
      ;; (a failing form errors the build by design), so the compiler isn't left in
      ;; optimize/direct-link mode for a later caller.
      (let*-values
          (((core-strs app-strs drop-compiler?)
            (dynamic-wind
              (lambda ()
                ;; Create + publish this build's compilation unit FIRST, so every
                ;; mode flag below lands on it (the unit the per-form emit reads).
                ;; The build emits app + core forms that reference clojure.core, which
                ;; must lower to var-deref, so prelude mode is on for the whole build.
                (ei-fresh-unit!)
                ((var-deref "jolt.backend-scheme" "set-prelude-mode!") #t)
                ;; The passes run for every mode but dev. "release" and
                ;; "optimized" differ only in the Chez compile parameters
                ;; below (inspector + procedure-source info), not in what the
                ;; compiler emits -- inlining follows direct-link, which both
                ;; of them set.
                (set-optimize! (not (string=? mode "dev")))
                (when direct-link?
                  ((var-deref "jolt.backend-scheme" "set-direct-link!") #t)
                  ((var-deref "jolt.backend-scheme" "direct-link-reset!"))
                  (set-direct-link-flag! #t))
                ;; Register each fn def's source, so an uncaught error in the built
                ;; binary maps its frames to "ns/name (file:line)" instead of a bare
                ;; procedure name. A direct-link build already emits these from
                ;; emit-def-cached, which gates on direct-link — so WITHOUT it a
                ;; built binary printed `deep-boom` where the direct-linked one
                ;; printed `app.util/deep-boom (…/util.clj:24)`. On only for the
                ;; open-world build, so a direct-link build's emitted bytes are
                ;; unchanged and nothing registers twice. The runtime eval path
                ;; turns this on for the same reason (compile-eval.ss); the seed
                ;; mint keeps it off, since its output must not carry this machine's
                ;; absolute paths (emit-image.ss).
                ((var-deref "jolt.backend-scheme" "set-source-reg!") (not direct-link?))
                ;; Bake tracing into built binaries by default (0.6.2): the tail-
                ;; site instrumentation is marks at user tail sites plus one vreg
                ;; store at native/throw tail sites — measured at ~1.0-1.3x of the
                ;; untraced floor — and the chain's site literals carry their own
                ;; lines, so a deployed binary's trace needs no marker files.
                ;; JOLT_TRACE=0 at BUILD time opts out (build-time axis, like the
                ;; dev-mode emission toggle; the baked binary has no runtime knob).
                ;; The seed mint is untouched — its emission stays untraced.
                ((var-deref "jolt.backend-scheme" "set-trace-frames!")
                 (not (jolt-trace-env-off? (getenv "JOLT_TRACE"))))
                ;; Cache resolved var cells per reference site in the APP forms
                ;; (bld-emit-ns / ei-emit-ns-records). A user build is a single
                ;; compile of fixed source, so the gensym-numbered cell names are
                ;; deterministic — the byte-fixpoint concern (the compiler re-
                ;; compiling itself) does NOT apply here, only to the seed mint,
                ;; which keeps var-cache OFF (emit-image.ss). ON in both modes.
                ((var-deref "jolt.backend-scheme" "set-var-cache!") #t)
                ;; whole-program param-type fixpoint before per-form emit — runs in
                ;; release and optimized (the inference modes). JOLT_NO_WP_INFER=1
                ;; skips it: a documented escape for very large apps where the
                ;; fixpoint's cost matters; emit then falls back to per-ns inference
                ;; (run-passes still annotates from explicit ^double/^long hints).
                (when (and (not (getenv "JOLT_NO_WP_INFER"))
                           (or (string=? mode "release") (string=? mode "optimized")))
                  (let ((wp-cached (bld-wp-infer! ordered)))
                    (for-each (lambda (p) (ei-set-cached! (car p) (cdr p))) wp-cached)))
                (ei-mark! "whole-program inference")
                (ei-acc-report!))
              (lambda ()
                ;; A #tag data-reader literal must compile in the binary the same as
                ;; it loads interpreted — apply the reader rewrite to each emitted
                ;; form too (no-op unless the app registered data readers).
                (parameterize ((ei-emit-form-hook
                                (lambda (form) (if data-readers-active (ldr-apply-readers form) form))))
                  ;; Every build emits its app namespaces as DCE records — the
                  ;; emitted Scheme plus the vars it references — and reads the
                  ;; prelude as records too, so the same reachability walk that
                  ;; drives --tree-shake also answers, for every build, whether
                  ;; the program can reach the compiler. A shake prunes on that
                  ;; graph; the default keeps every record and takes only the
                  ;; compiler verdict (dce-needs-compiler?).
                  ;;
                  ;; EAGER per-ns accumulation, NOT (apply append (map …)):
                  ;; `map` here is jolt's LAZY map, and the per-ns emit lambdas
                  ;; carry side effects — analysis, cell/gensym allocation,
                  ;; direct-link-defined registration — whose realization order
                  ;; under apply/append is not the list order, so an entry-ns
                  ;; lambda could run before a dependency's (var-routed calls,
                  ;; out-of-order cells). The named let runs strictly in
                  ;; `ordered` order (deps first), matching the loader.
                  (let ((core-records (dce-blob-records "host/chez/seed/prelude.ss"))
                        (app-records
                          (let ((per-ns '()))
                            (let loopfe ((rest ordered))
                              (unless (null? rest)
                                (let* ((nf (car rest))
                                       (src (ei-timed "emit: read source"
                                              (lambda () (ldr-read-source (cdr nf)))))
                                       (profile-form
                                         (bld-startup-profile-form
                                           (string-append "namespace " (car nf)))))
                                  (jolt-enter-file! (cdr nf))   ; name the file on a failure
                                  (parameterize ((rdr-source-file (cdr nf)))
                                    ;; RT.load-parity bracket (dyn-binding.ss): the
                                    ;; ns's replayed forms run under fresh
                                    ;; *warn-on-reflection*/*assert* bindings.
                                    (set! per-ns
                                      (cons (append
                                              (list (dce-rec #t #f '() "(jolt-ns-load-vars-push!)"))
                                              (map (lambda (s) (dce-rec #t #f '() s))
                                                   (ei-timed "emit: ns-prelude"
                                                     (lambda () (bld-ns-prelude (car nf) src))))
                                              (ei-timed "emit: per-ns total"
                                                (lambda () (ei-emit-ns-records (car nf) src)))
                                              (list
                                                (dce-rec #t #f '() "(jolt-ns-load-vars-pop!)")
                                                (dce-rec #t #f '() profile-form)))
                                            per-ns)))
                                (loopfe (cdr rest))))
                            (apply append (reverse per-ns)))))
                        (entry-main (string-append entry-ns "/-main")))
                    (if tree-shake?
                        (dce-shake core-records app-records entry-main allow-dynamic)
                        (values #f
                                (map dce-rec-str app-records)
                                (not (dce-needs-compiler? core-records app-records
                                                          entry-main allow-dynamic)))))))
              (lambda ()
                (set-optimize! #f)
                (set-direct-link-flag! #f)
                ((var-deref "jolt.backend-scheme" "set-direct-link!") #f)
                ((var-deref "jolt.backend-scheme" "set-source-reg!") #f)
                ;; restore the DEV-mode emission state, not #f — an in-process
                ;; build (nREPL, cmd-build without exec) must not turn off tracing
                ;; for code the session compiles afterwards.
                ((var-deref "jolt.backend-scheme" "set-trace-frames!") jolt-trace-on?)
                ;; drop the accumulated direct-link fqn set too — a later
                ;; in-process build would otherwise bind calls against defs
                ;; recorded for THIS one. (bld-wp-infer!'s record/protocol
                ;; seeds self-heal: the next build replaces them wholesale.)
                ((var-deref "jolt.backend-scheme" "direct-link-reset!"))
                ((var-deref "jolt.backend-scheme" "set-var-cache!") #f)
                ;; clear the build unit's record-shapes: the emit pointer still
                ;; points at this finished build unit, and the direct-ctor emit
                ;; (make-jrecN) is gated only on shape presence, not direct-link —
                ;; so a hypothetical in-process build-then-eval would otherwise fire
                ;; it off stale build-time shapes. Harmless under today's control
                ;; flow (build XOR eval per process), cheap to make robust.
                (jolt-wp-set-record-shapes! (ei-unit) (jolt-hash-map))
                (ei-clear-cached!)))))
        (when drop-compiler? (display "jolt build: dropping compiler image (no runtime eval)\n"))
      (ei-mark! "emit app namespaces")
      (ei-acc-report!)
      (let* ((builddir (string-append out-path ".build"))
             (flat-ss  (string-append builddir "/flat.ss"))
             (flat-so  (string-append builddir "/flat.so"))
             (rt-ss    (string-append builddir "/runtime.ss"))
             (rt-so    (string-append builddir "/runtime.so"))
             (boot     (string-append builddir "/jolt.boot"))
             (boot-h   (string-append builddir "/boot_data.h"))
             (main-c   (string-append builddir "/main.c"))
             ;; Emit the runtime half to its own file, always: it compiles under
             ;; its own Chez parameters (bld-runtime-chez-params — no inspector
             ;; information, which is 57% of a release binary and nothing the
             ;; runtime's frames ever read), and when it is app-independent its
             ;; fasl is cached (bld-compile-runtime!). A shaken core (core-strs)
             ;; is per-app, so that unit skips the cache but is still the runtime
             ;; unit; the cc / cross / library paths compile both files in their
             ;; spawned Chez. JOLT_NO_FLAT_SPLIT=1 forces the one-file form
             ;; everywhere — an escape hatch for telling a build problem caused
             ;; by the split apart from one merely revealed by it — and the one
             ;; file then compiles under the app half's parameters.
             (split? (not (getenv "JOLT_NO_FLAT_SPLIT")))
             (units (cond ((not split?) (list (list flat-ss flat-so 'whole)))
                          (core-strs (list (list rt-ss rt-so 'runtime-shaken)
                                           (list flat-ss flat-so 'app)))
                          (else (list (list rt-ss rt-so 'runtime)
                                      (list flat-ss flat-so 'app))))))
        (bld-mkdir-p builddir)
        ;; 3. flat source = runtime + app + launcher. When split, runtime.ss holds
        ;; the runtime half and flat.ss holds everything the app contributes; the
        ;; two are compiled separately and loaded into the boot in that order.
        (when split?
          (let ((out (open-output-file rt-ss 'replace)))
            ;; No mode in the content: the runtime half compiles under ONE fixed
            ;; parameter profile whatever the mode (bld-runtime-chez-params), so
            ;; release, --opt and --dev share a single cache entry.
            (put-string out ";; jolt runtime half\n")
            (bld-emit-runtime out drop-compiler? core-strs)
            (close-port out)))
        (let ((out (open-output-file flat-ss 'replace)))
          (if split?
              (put-string out ";; app half — the runtime half is compiled separately (runtime.ss)\n")
              (bld-emit-runtime out drop-compiler? core-strs))
          ;; Load native libs, bake embedded resources, and point source roots at
          ;; the build-time app roots — all BEFORE the app forms. The app's
          ;; top-level forms run at binary startup (Sbuild_heap), and they include
          ;; foreign-procedure evals (a library's defcfn) and (slurp (io/resource …))
          ;; reads. So the libraries must be loaded and resources resolvable by the
          ;; time those forms run, not later in the scheme-start launcher.
          (bld-emit-startup-profile-mark! out "app image begin")
          (put-string out "\n;; === native libraries (required) ===\n")
          (bld-emit-natives out natives 'required)
          (bld-emit-startup-profile-mark! out "required native libraries")
           (put-string out "\n;; === embedded resources ===\n")
           (bld-emit-embeds out embed-dirs)
            (bld-emit-data-readers out)
           ;; set-source-roots!* (not the scanning set-source-roots!): data readers
           ;; are baked just above, and re-scanning would eagerly reload reader
           ;; namespaces via jolt-compile-eval-form — dropped by a tree-shaken binary.
           (put-string out (string-append
                             "(set-source-roots!* (list "
                             (fold-left (lambda (s r) (string-append s (ei-str-lit r) " ")) ""
                                        (get-source-roots))
                             "))\n"))
          (bld-emit-startup-profile-mark! out "embedded resources and source roots")
          ;; Pre-register every app namespace in ns-registry BEFORE any app form
          ;; runs, so a boot-time (require 'x) of an AOT'd namespace no-ops (the
          ;; loader's ns-registry arm) instead of hunting for absent source. Needed
          ;; when a namespace requires a later one at load time — e.g.
          ;; babashka.process conditionally requires babashka.process.pprint, which
          ;; defines no vars of its own (only a defmethod) so ns-has-vars? can't
          ;; vouch for it and its own (ns) form hasn't run yet.
          (put-string out "\n;; === app namespace pre-registration ===\n")
          (for-each (lambda (p) (put-string out (string-append "(intern-ns! " (ei-str-lit (car p)) ")\n")))
                    ordered)
          (bld-emit-startup-profile-mark! out "app namespace registration")
          ;; The app's forms are DECLARED here and RUN from the launcher — see
          ;; bld-defer-app-strs. The profile mark rides along into the init body,
          ;; so "app namespaces begin" still brackets the work rather than the
          ;; declarations.
          (put-string out "\n;; === app (declarations; the bodies run at scheme-start) ===\n")
          (let-values (((decls bodies) (bld-defer-app-strs app-strs)))
            (for-each (lambda (s) (put-string out s) (put-string out "\n")) decls)
            (bld-emit-app-init out (cons (bld-startup-profile-form "app namespaces begin") bodies)))
          ;; The launcher runs as Chez's scheme-start (so argv reaches -main —
          ;; top-level boot forms run during heap build, before args are set), and
          ;; suppresses the interactive greeting. It resets source roots to the
          ;; app's resource dirs resolved against JOLT_PWD (or cwd) so a runtime
          ;; io/resource that wasn't embedded still resolves next to the binary.
          (put-string out "\n;; === launcher ===\n")
          (put-string out "(suppress-greeting #t)\n")
          ;; GC tuning: larger nursery for allocation-heavy workloads (binary-trees,
          ;; ray tracer, etc.). Default 16 MB; override via JOLT_GC_TRIP_BYTES
          ;; environment variable (integer bytes, e.g. \"33554432\" for 32 MB).
          (put-string out
            (string-append
              "(sa-gc-trip-bytes!\n"
              "  (let ((trip (getenv \"JOLT_GC_TRIP_BYTES\"))\n"
              "        (default (* 16 1024 1024)))\n"
              "    (if trip (or (string->number trip) default) default)))\n"
              ;; and a heap ceiling, so a built app fails with an
              ;; OutOfMemoryError carrying a stack rather than being SIGKILLed
              ;; by the kernel with nothing to read. Same contract as jolt's own
              ;; launcher and as the JVM's MaxRAMPercentage default.
              "(jolt-install-heap-ceiling!)\n"))
          (put-string out "(scheme-start\n  (lambda args\n")
          (bld-emit-startup-profile-mark! out "scheme-start begin")
          ;; Shutdown hooks (`:shutdown` on a jolt.process, jolt.host/
          ;; add-shutdown-hook) run from Chez's exit-handler, which is a THREAD
          ;; parameter — so the wrapper has to be installed on the thread that
          ;; calls (exit), and for an app that is this one. Installed before the
          ;; guard so the (exit 1) an uncaught throw takes runs the hooks too.
          ;; The CLI's own twin of this is at the top of jolt-cli-run.
          (unless library? (put-string out "    (jolt-install-exit-handler!)\n"))
          ;; The prologue (optional native loads + source-root setup) and the -main
          ;; call (or library export publish) run under one guard so a throw in
          ;; either surfaces as jolt-report-throwable + a non-zero exit/return
          ;; instead of Chez's opaque dump — the prologue previously ran before any
          ;; guard. A library returns 1 (so Sscheme_start returns non-zero to its
          ;; caller); an executable exits 1.
          (put-string out
            (string-append
              "    (guard (v (#t (jolt-report-throwable v (current-error-port))"
              (if library? " 1))\n" " (exit 1)))\n")))
          ;; The app's own top-level forms, first thing inside the guard: past
          ;; Sbuild_heap (so a thread they spawn can actually run) but still
          ;; before the optional natives and the runtime source-root reset, which
          ;; is the order they ran in when they lived in the boot file. Being
          ;; inside the guard is a bonus — a throw from an app top-level form used
          ;; to escape as Chez's opaque dump, and now reports like any other.
          (put-string out "      (jolt-app-init!)\n")
          (bld-emit-natives out natives 'optional)
           (put-string out (string-append
                              "      (let ((base (or (getenv \"JOLT_PWD\") \".\")))\n"
                              "        (set-source-roots!*\n"
                              "          (append (map (lambda (r) (string-append base \"/\" r)) (list "
                             (fold-left (lambda (s r) (string-append s (ei-str-lit r) " ")) "" (bld-strs ext-roots))
                             "))\n"
                             "                  " (ldr-install-roots-str) ")))\n"))
          (bld-emit-startup-profile-mark! out "scheme-start setup")
          (if library?
              (put-string out (bld-library-launcher-body))
              (put-string out (string-append
                            ;; Call -main only if the entry namespace defines one;
                            ;; a script ns (top-level side effects, no -main) has
                            ;; already run its forms at heap build, so invoking a nil
                            ;; -main would crash ("nil cannot be cast to IFn") — just
                            ;; exit cleanly instead.
                            "      (let ((maincell (var-cell-lookup " (ei-str-lit entry-ns) " \"-main\")))\n"
                            ;; Loading the app left the current ns at the entry ns; reset
                            ;; it to `user` before -main, matching clojure.main (*ns* is
                            ;; `user` when a `-m` -main runs, so a runtime resolve of an
                            ;; aliased symbol behaves the same as on the JVM / interpreted
                            ;; jolt, not off the entry ns's alias table).
                            "        (set-chez-ns! \"user\")\n"
                            ;; The same host-fault capture the cli's run path has
                            ;; (cli-core.ss jolt-cli-run): a raw Chez condition gets
                            ;; its k/marks/site stashed BEFORE the unwind, so a
                            ;; traced binary maps the fault to fn + line. jolt
                            ;; throws skip it (jolt-capture-fault! tests) and
                            ;; raise-continuable preserves warning semantics.
                            "        (when (and maincell (var-cell-defined? maincell))\n"
                            "          (with-exception-handler\n"
                            "            (lambda (c) (when (serious-condition? c) (jolt-capture-fault! c)) (raise-continuable c))\n"
                            "            (lambda ()\n"
                            "              (let ((jolt-main-result (apply jolt-invoke (var-cell-root maincell) args)))\n"
                            "                " (bld-startup-profile-form "entry -main") "\n"
                            "                jolt-main-result))))))\n"
                            ;; as the CLI: a non-daemon Thread the program started
                            ;; keeps the process alive until it finishes
                            "    (jolt-await-user-threads!)\n"
                            "    (exit 0)))\n")))
          (close-port out))
        (ei-mark! "write flat.ss")
        ;; 4. compile -> boot -> link. Two paths, chosen by whether this process
        ;; carries the bundled Chez boots + launcher stub:
        ;;  - SELF-CONTAINED (the distributed jolt, jolt-eaj): compile-file +
        ;;    make-boot-file run IN PROCESS (the compiler is resident — jolt is
        ;;    built from scheme.boot), then the boot is appended to a copy of the
        ;;    embedded stub. No external Chez, no cc.
        ;;  - LEGACY (dev bin/jolt): spawn a fresh Chez for compile-file/
        ;;    make-boot-file, then xxd the boot into a C array and cc-link against
        ;;    libkernel.a. Kept so `make buildsmoke` still exercises the cc path.
        (cond
          ;; Cross-compiling (--target) always takes a spawned cc path: the
          ;; self-contained in-process compile can't load a target xpatch, and the
          ;; xpatch retargets make-boot-file for the whole spawned process.
          ((and (bld-cross?) library?)
           (build-shared entry-ns out-path mode builddir units boot boot-h ""))
          ((bld-cross?)
           (build-with-cc entry-ns out-path mode builddir units boot boot-h main-c
                          "" (and drop-compiler? (not (bld-tgt-nt?)))))
          (library?
           (build-shared entry-ns out-path mode builddir units boot boot-h
                         (bld-native-link-flags natives)))
          ;; petite-only is POSIX-only: on Windows jolt-foreign-proc-safe still
          ;; evals its foreign-procedure forms (fasl relocations abort the boot
          ;; there), and eval needs the compiler boot resident.
          ((jolt-embedded-bytes "stub/launcher")
           (build-self-contained entry-ns out-path mode builddir units boot
                                 (bld-native-link-flags natives)
                                 (and drop-compiler? (not bld-nt?))))
          (else
           (build-with-cc entry-ns out-path mode builddir units boot boot-h main-c
                          (bld-native-link-flags natives)
                          (and drop-compiler? (not bld-nt?)))))))))))

;; --- self-contained link (in-process compile + append the boot to the stub) ---
;; compile-file runs against the DEFAULT interaction environment, so the boot's
;; top-level defines land in the real symbol cells — the runtime compiler's
;; eval'd code must resolve them (var-deref, jolt-invoke, the jolt-n* macros)
;; when the built binary dynamically requires a namespace. Compiling in a clean
;; copy-environment instead orphans every define in locations eval can't see,
;; and the binary dies with "variable var-deref is not bound" the moment a
;; runtime require compiles source.
;;
;; The default env has a wrinkle the legacy fresh-Chez path doesn't: THIS
;; process's cells hold jolt's redefinitions of some kernel names (`error`,
;; regex.ss), so references to them compile as cell reads — and a read that
;; runs before the redefining form would find the fresh binary's cell unbound.
;; The prologue closes that: it first binds each redefined kernel name's cell
;; to its kernel value, making the boot's earliest reads identical to the
;; legacy path's primitive references.

;; every top-level (define nm …)/(define (nm …) …) name in the flat file that
;; shadows a scheme-environment VARIABLE (syntax names don't eval; skip them).
(define (bld-kernel-prologue flat-ss)
  (let ((seen (make-eq-hashtable))
        (kenv (scheme-environment))
        (names '()))
    (let ((ip (open-input-file flat-ss)))
      (let loop ()
        (let ((f (read ip)))
          (unless (eof-object? f)
            (when (and (pair? f) (eq? (car f) 'define) (pair? (cdr f)))
              (let* ((h (cadr f))
                     (nm (if (pair? h) (car h) h)))
                (when (and (symbol? nm)
                           (not (hashtable-ref seen nm #f))
                           (guard (e (#t #f)) (begin (eval nm kenv) #t)))
                  (hashtable-set! seen nm #t)
                  (set! names (cons nm names)))))
            (loop))))
      (close-port ip))
    (apply string-append
           (map (lambda (nm)
                  (let ((s (symbol->string nm)))
                    (string-append "(define " s " (eval '" s " (scheme-environment)))\n")))
                (reverse names)))))

;; prepend the prologue to the flat file in place, then bake the runtime
;; fingerprint the AOT namespace cache keys on (loader.ss). An app binary carries
;; no version string, so without this every one of them would key its cached
;; fasls under the same "dev" and load namespaces another binary's runtime had
;; emitted. This file is the binary's runtime, so its content hash names it.
;; The fingerprint covers the header + prologue + body, i.e. the file as it stands
;; before the fingerprint itself is appended — so it is assembled in memory once
;; and written once, rather than writing the file, reading it back to hash it, and
;; appending. Same bytes, same fingerprint, one pass over a multi-megabyte file
;; instead of three.
(define (bld-prepend-prologue! flat-ss)
  (let* ((prologue (bld-kernel-prologue flat-ss))
         (src (string-append
                ";; kernel-name cells pre-bound so early reads match the kernel primitives\n"
                prologue
                (read-file-string flat-ss)))
         (fp (string-append (number->string (string-length src) 16) "-"
                            (number->string (aot-content-hash src) 16)))
         (out (open-output-file flat-ss 'replace)))
    (put-string out src)
    (put-string out (string-append
                      "\n;; === runtime fingerprint (AOT cache key) ===\n"
                      "(define jolt-baked-runtime-fingerprint " (ei-str-lit fp) ")\n"))
    (close-port out)))

;; Per-mode Chez compile parameters for the APP half of a binary (the app and
;; its libraries). "release" keeps inspector + proc-source information ON:
;; Chez records a frame's return-point source only as inspector information,
;; and the reporter resolves that offset through the marker table to recover
;; the spliced chain a frame sits in and the exact line — the `step-boom` /
;; `app.util/inner-boom` frames the build smoke's --innerfn case pins vanish
;; without it (both parameters off, AND proc-source alone: it does not cover
;; return points). "optimized" turns them OFF for the smallest, fastest app
;; half; "dev" has no entry (Chez defaults: optimize-level 2, inspector ON,
;; proc-source ON, fasl uncompressed — full debuggability). Single table
;; referenced by both the script-string builder and the in-process compile.
;;
;; The RUNTIME half never uses this table — see bld-runtime-chez-params.
;;
;; optimize-level 2, not 3: level 3 is Chez's UNSAFE mode — fx/fl/car/vector
;; ops skip their type checks, and jolt's error semantics depend on those
;; raising ((take nil coll) must throw, not walk off a nil count). Level 2
;; keeps every check with nearly all of the optimization.
(define bld-chez-params
  '(("optimized" (optimize-level 2)
                 (generate-inspector-information #f)
                 (generate-procedure-source-information #f)
                 (fasl-compressed #t))
    ("release"   (optimize-level 2)
                 (generate-inspector-information #t)
                 (generate-procedure-source-information #t)
                 (fasl-compressed #t))))

;; The RUNTIME half's parameters — rt.ss, the clojure.core prelude, the
;; compiler image, the loader — one profile whatever the mode, and the same
;; one jolt's own binary is built with (build-jolt.ss, release): no inspector
;; information, no procedure-source information. Nothing reads them there: a
;; runtime frame prints by its code name (na-chunk-map-first, map-seq, dorun),
;; which Chez keeps either way; core is minted without splicing, so it has no
;; inline chains to recover; and the image writer learns a core closure's
;; capture layout from its maker, not from inspector names. What they cost
;; was the whole of burinc/jolt#3: with the release row's parameters over the
;; runtime half too, a hello-world binary measured 27.25MB, 110ms to start and
;; 225MB resident, against 11.68MB / 70ms / 132MB with them off — inspector
;; information was 57% of the bytes, and the `Sbuild_heap` phase that scales
;; with the image (90MB → 48MB of heap once decompressed) 106ms → 44ms.
;; build-smoke pins the runtime half byte-identical across release and --opt,
;; so the profile cannot quietly drift back to per-mode.
(define bld-runtime-chez-params
  '((optimize-level 2)
    (generate-inspector-information #f)
    (generate-procedure-source-information #f)
    (fasl-compressed #t)))

;; PARAMS as `(name value)` binding text — the body of a parameterize, or one
;; form per line when SEP is a newline.
(define (bld-params-bindings params sep)
  (fold-left
    (lambda (acc p)
      (string-append acc (if (string=? acc "") "" sep)
                     "(" (symbol->string (car p)) " "
                     (let ((v (cadr p)))
                       (cond ((boolean? v) (if v "#t" "#f"))
                             ((number? v) (number->string v))
                             (else (format "~s" v))))
                     ")"))
    "" params))

;; The app half's parameters for MODE, or #f for a mode with no row (dev).
(define (bld-mode-params mode)
  (let ((row (assoc mode bld-chez-params))) (and row (cdr row))))

;; A `(compile-file SRC SO)` form for a spawned Chez, under PARAMS when given.
(define (bld-compile-file-form params src so)
  (let ((cf (string-append "(compile-file " (ei-str-lit src) " " (ei-str-lit so) ")")))
    (if params
        (string-append "(parameterize (" (bld-params-bindings params " ") ")\n  " cf ")\n")
        (string-append cf "\n"))))

;; The compile forms for UNITS in a spawned Chez: the runtime unit under the
;; runtime profile, the app (or one-file) unit under the mode's row.
(define (bld-units-compile-forms units mode)
  (fold-left
    (lambda (acc u)
      (string-append acc
        (bld-compile-file-form
          (if (memq (caddr u) '(runtime runtime-shaken)) bld-runtime-chez-params (bld-mode-params mode))
          (car u) (cadr u))))
    "" units))

;; Every unit's object file, quoted, in load order — the make-boot-file tail.
(define (bld-units-so-args units)
  (fold-left (lambda (acc u) (string-append acc "  " (ei-str-lit (cadr u)) "\n")) "" units))

;; Compile SRC to SO in this process under PARAMS (an alist as above), by
;; translating the parameter names into the target-neutral profile
;; sa-compile-file consumes; #f = the target's defaults.
(define (bld-chez-compile-params! params src so)
  (if params
      (let ((pv (lambda (k) (cadr (assq k params)))))
        (sa-compile-file src so
          `((optimize . ,(pv 'optimize-level))
            (inspector-info . ,(pv 'generate-inspector-information))
            (source-info . ,(pv 'generate-procedure-source-information))
            (compressed . ,(pv 'fasl-compressed)))))
      (sa-compile-file src so #f)))

;; Compile one app-half (or one-file) source under MODE's row.
(define (bld-chez-compile-file mode src so)
  (bld-chez-compile-params! (bld-mode-params mode) src so))

;; --- runtime-half fasl cache -------------------------------------------------
;; The runtime half of the flat source (rt.ss + the clojure.core prelude +
;; host-contract + the compiler image + loader + ffi) is byte-identical for every
;; app a given jolt builds in a given mode — only its trailing set-source-roots!
;; depends on the install, not on the app. Compiling it is ~2.6s, which for a
;; small app is most of the build. Compile it once per (content, mode) and keep
;; the fasl, so that cost is paid on the first build and never again.
;;
;; Keyed on the content of the runtime source BEFORE bld-prepend-prologue! runs,
;; because both the kernel prologue and the baked fingerprint are deterministic
;; functions of that content — so the key identifies the finished fasl exactly,
;; and a hit skips the prologue pass as well as the compile.
;;
;; NOT used when --tree-shake rewrites the prelude (the runtime half becomes
;; app-specific), for a --library or cross build, or on the legacy cc path.
;; Its own directory and its own variable, deliberately not JOLT_CACHE_DIR: that
;; one names the AOT namespace cache, and pointing both at one directory would
;; leave each pruning around the other's files.
(define (bld-runtime-cache-dir)
  (or (getenv "JOLT_RUNTIME_CACHE_DIR")
      (string-append (or (getenv "HOME") ".") "/.jolt/runtime-cache")))
(define (bld-runtime-cache-enabled?)
  (let ((e (getenv "JOLT_RUNTIME_CACHE")))
    (if (and (string? e) (fx>? (string-length e) 0))
        (not (or (string=? e "0") (string-ci=? e "false")
                 (string-ci=? e "no") (string-ci=? e "off")))
        #t)))
;; Keyed on the source AND the Chez parameters it compiles under
;; (bld-runtime-chez-params): the parameters are what decide the fasl's
;; bytes. Keyed on the mode's name alone, as this was, the day the runtime
;; half stopped generating inspector information every build on a machine
;; that had built before kept serving the old 22MB fasl under the new policy
;; — the same source, the same word "release", a different compile — and the
;; binary did not shrink until the cache was cleared by hand.
;; …and on the Chez that compiles it: a fasl is specific to the kernel's version
;; and host (the machine-type tag, through the adapter), and two jolt binaries
;; with different bundled kernels share this directory. Identical source text
;; under a newer kernel must miss.
(define (bld-runtime-cache-path body)
  (let ((keyed (string-append (scheme-version) " " (sa-host-tag) "\n"
                              (bld-params-bindings bld-runtime-chez-params "\n") body)))
    (string-append (bld-runtime-cache-dir) "/runtime-"
                   (number->string (string-length body) 16) "-"
                   (number->string (aot-content-hash keyed) 16) ".so")))
;; Keep the newest few entries. One accumulates per jolt build × mode, so a
;; developer re-minting often would otherwise grow this without bound.
(define bld-runtime-cache-keep 8)
(define (bld-prune-runtime-cache!)
  (guard (e (#t #f))
    (let* ((dir (bld-runtime-cache-dir))
           (fs (map (lambda (f) (let ((p (string-append dir "/" f)))
                                   (cons p (sa-file-mtime-ms p))))
                     (filter (lambda (f) (bld-suffix? f ".so")) (directory-list dir)))))
      (when (> (length fs) bld-runtime-cache-keep)
        (for-each (lambda (p) (guard (e (#t #f)) (delete-file (car p))))
                  (list-tail (sort (lambda (a b) (> (cdr a) (cdr b))) fs)
                             bld-runtime-cache-keep))))))
;; Remove an existing output before writing the new one, so the new binary lands on
;; a FRESH inode.
;;
;; macOS caches a code-signature verdict per vnode. Rewriting an executable in place
;; leaves the stale verdict attached to it, and the kernel then SIGKILLs the next run
;; with no output whatsoever — `Killed: 9`, exit 137, nothing on stderr. A
;; rebuild-and-run loop over one output path (the build smoke; anyone iterating on an
;; app) therefore works a handful of times and then starts dying for no visible
;; reason, on a binary that runs fine the moment it is built somewhere else.
;;
;; It also means a failed link leaves no output rather than a half-overwritten one.
(define (bld-clear-output! out-path)
  (when (file-exists? out-path) (delete-file out-path)))

(define (bld-copy-file! from to)
  (let ((bs (read-file-bytes from)))
    (let ((out (open-file-output-port to (file-options no-fail))))
      (put-bytevector out bs)
      (close-port out))))

;; Compile the runtime half under the runtime profile, reusing a cached fasl
;; when one matches. CACHE? is #f for a shaken core: its text is per-app, so a
;; hit is impossible and a store would only churn the cache.
(define (bld-compile-runtime! src so cache?)
  (let* ((body (read-file-string src))
         (cache (and cache? (bld-runtime-cache-enabled?) (bld-runtime-cache-path body))))
    (if (and cache (file-exists? cache))
        (begin
          (bld-copy-file! cache so)
          (ei-mark! "runtime fasl (cached)"))
        (begin
          (bld-prepend-prologue! src)
          (ei-mark! "kernel prologue + hash")
          (bld-chez-compile-params! bld-runtime-chez-params src so)
          (ei-mark! "compile runtime half")
          (when cache
            (guard (e (#t #f))          ; an unwritable cache must not fail the build
              (bld-mkdir-p (bld-runtime-cache-dir))
              (bld-copy-file! so cache)
              (bld-prune-runtime-cache!)))))))

;; --- how the boot image is encoded: --boot (jolt-lang/jolt#886) -------------
;; Three points on one curve, and the flag is ordered along it:
;;
;;   'fast    vfasl + LZ4   the default — the fastest start, the largest binary
;;   'small   vfasl + gzip  still an image, but a third smaller than a PLAIN boot
;;                          and still faster to start than one
;;   'plain   no vfasl      the fasl stream 0.8.4 produced (`--no-vfasl`)
;;
;; A vfasl boot is an image of the loaded heap, so it starts fast and takes room;
;; that cost is what jolt#886 hit, an iOS `--target tpb64l` build growing 7.6MB in
;; the binary and ~5MB in the IPA. But the cost turns out to be mostly the
;; CODEC's, not vfasl's. Measured over two apps and two machine types, binary
;; size and warm start against the plain boot as the baseline:
;;
;;   hello, host ta6le        plain 25,919,203/495ms  lz4 +5.5%/249ms  gzip -35.7%/429ms
;;   build-app, host ta6le    plain 26,062,746/502ms  lz4 +5.8%/250ms  gzip -35.5%/434ms
;;   hello, target tpb64l     plain 24,873,035        lz4 +5.8%        gzip -38.1%
;;
;; So for a jolt app 'small beats 'plain on BOTH axes and 'plain is a floor
;; nobody should want — which is why it stays available (a target that cannot
;; vfasl at all still needs it) but is not what the size-conscious build should
;; reach for. The ratios are a property of what is in the image, not of the
;; machine: the same three encodings over Chez's own boots, which carry no jolt
;; runtime, cost lz4 +37% and gain gzip only 3-4%, with gzip SLOWER than plain.
;; Anything user-facing has to say measure your own app, not quote one ratio.
;;
;; The mode is resolved once, in jolt.main (CLI flag > deps.edn > JOLT_BOOT /
;; JOLT_NO_VFASL > default) and passed down; nothing here re-reads the
;; environment, so there is one precedence rule rather than two.
(define bld-boot-mode (make-parameter 'fast))

(define (bld-vfasl-disabled?) (eq? (bld-boot-mode) 'plain))

;; --- the boot image's LZ4 ceiling -------------------------------------------
;; A big enough compressed fasl entry cannot be read back by the Chez kernel when
;; the entry is LZ4. c/new-io.c's S_bytevector_uncompress returns the
;; decompressed length as `Sfixnum(r)` with `int r`, and Sfixnum is
;; `((ptr)(uptr)((x)*8))` — the multiply happens in the argument's own type, so
;; the product leaves 32 bits and the length check in c/fasl.c can never match.
;; The load then dies inside Sbuild_heap with "uncompressed size N ... is smaller
;; than expected size M", before a line of the binary's own code has run. The
;; gzip arm of the same function hands zlib a uLong and has no such ceiling.
;;
;; WHERE the line falls is undefined behaviour, and it is not the same on every
;; platform, because what the widening cast does with the top bit of an overflowed
;; `int` is the C compiler's business. Both of these are Chez 10.4.1
;; (test/chez/vfasl-ceiling-test.ss measures whichever one is in front of it):
;;
;;   sign-extended   a NEGATIVE length at 2^28    ceiling 2^28   ta6le, and the
;;                   platform in jolt-lang/jolt#886 — its -222298112 is exactly
;;                   314572800*8 wrapped to signed 32-bit and divided back by 8
;;   zero-extended   a length of 0 at 2^29        ceiling 2^29   tarm64osx
;;
;; 2^28 is therefore jolt's FLOOR, not a measurement of the machine: at or below
;; every ceiling seen, so nothing it leaves on LZ4 can fail to load. On a
;; zero-extending platform an image between 2^28 and 2^29 is re-encoded when it
;; did not have to be, which costs decompression speed and nothing else. That is
;; the direction to be wrong in, and it is why nothing user-facing quotes a size.
;;
;; It is 0.8.5's vfasl boot that can reach the ceiling at all. A plain boot is
;; one compressed entry per top-level form and its entries are kilobytes;
;; vfasl-convert-file combines each input boot file into ONE entry, so the app
;; half of a large program is a single image — jolt's own is 43MB, and a program
;; six times that size stops booting rather than merely booting slowly.
;;
;; jolt cannot fix the kernel it links against (an installed Chez is the user's),
;; so it keeps the image off the ceiling instead: measure the converted boot and
;; re-encode with gzip when an LZ4 entry is over. Only the oversized build pays
;; gzip's slower decompression, and it pays it in exchange for booting at all.
;;
;; A parameter rather than a constant for one reason: the fallback below is code
;; that runs only for images no gate can afford to build, and that is exactly the
;; code that rots. test/chez/vfasl-ceiling-test.ss lowers the ceiling and drives
;; the whole path against a boot it can build in a second. Nothing in a build
;; moves it.
(define bld-lz4-image-ceiling (make-parameter (expt 2 28)))

;; The largest uncompressed size any LZ4 entry of the boot file PATH declares, or
;; 0 when it has none. #f when the bytes do not parse as the framing below, which
;; a caller reads as "leave this boot alone" — a Chez whose fasl layout moved is
;; not something to guess at.
;;
;; Framing, from ChezScheme s/strip.ss (read-entry) and c/fasl.c:
;;   header entry  0 <7 more header bytes> <uptr version> <uptr machine> ( … )
;;   object entry  <situation 35|36|37> <uptr size> <u8 codec> <u8 kind>
;;                 codec 45 gzip / 46 lz4: <uptr uncompressed-size> <payload>
;;                 codec 44 uncompressed:  <payload>
;;                 SIZE counts the codec and kind bytes and everything after.
;;   terminator    127
;; A boot holds several headers — the one emit-boot-header writes, then the one
;; each input boot carried — so a header is a thing to skip, not an end.
(define (bld-boot-max-lz4-entry path)
  (define (u8 p)
    (let ((b (get-u8 p)))
      (if (eof-object? b) (error 'bld-boot-max-lz4-entry "eof in boot file" path) b)))
  ;; -> (values value bytes-read), the shape the SIZE arithmetic needs
  (define (uptr p)
    (let loop ((k (u8 p)) (n 0) (c 1))
      (let ((n (+ (* n 128) (bitwise-and k #x7f))))
        (if (= 0 (bitwise-and k #x80))
            (values n c)
            (loop (u8 p) n (+ c 1))))))
  (define (skip-header! p)               ; the leading 0 is already consumed
    (let loop ((i 1)) (when (< i 8) (u8 p) (loop (+ i 1))))
    (uptr p)                             ; version
    (uptr p)                             ; machine type
    (u8 p)                               ; #\(
    (let loop () (unless (= (u8 p) 41) (loop))))          ; through #\)
  (define (skip! p n)
    (when (< n 0) (error 'bld-boot-max-lz4-entry "negative entry size" path))
    (set-port-position! p (+ (port-position p) n)))
  (guard (e (#t #f))
    (let ((p (open-file-input-port path)))
      (guard (e (#t (close-port p) (raise e)))
        (let loop ((biggest 0))
          (let ((b (get-u8 p)))
            (cond
              ((eof-object? b) (close-port p) biggest)
              ((= b 0) (skip-header! p) (loop biggest))          ; another header
              ((= b 127) (loop biggest))                         ; terminator
              ((or (= b 35) (= b 36) (= b 37))                   ; visit/revisit/both
               (let-values (((size size-bytes) (uptr p)))
                 (let ((codec (u8 p)))
                   (u8 p)                                        ; kind: fasl|vfasl
                   (cond
                     ((or (= codec 45) (= codec 46))
                      (let-values (((raw raw-bytes) (uptr p)))
                        (skip! p (- size 2 raw-bytes))
                        (loop (if (and (= codec 46) (> raw biggest)) raw biggest))))
                     ((= codec 44)
                      (skip! p (- size 2))
                      (loop biggest))
                     (else (error 'bld-boot-max-lz4-entry "unknown fasl codec" codec))))))
              (else (error 'bld-boot-max-lz4-entry "unknown fasl entry type" b)))))))))

;; Does BOOT hold an LZ4 entry the kernel could not read back? #f for a boot that
;; does not parse: the status quo already works for every image under the
;; ceiling, and re-encoding on a guess would be the riskier answer. It says so
;; out loud, though — a silent "could not check" is how this bug would come back
;; wearing the same unreadable Sbuild_heap death it wore the first time.
(define (bld-boot-over-lz4-ceiling? boot)
  (let ((biggest (bld-boot-max-lz4-entry boot)))
    (cond ((not biggest) (bld-note-unscannable-boot! boot) #f)
          (else (>= biggest (bld-lz4-image-ceiling))))))

;; "at or over" rather than a size: where the kernel actually gives out is
;; undefined behaviour and moves by platform (2^28 or 2^29 — see
;; bld-lz4-image-ceiling), so the number jolt acts on is its own floor, not a
;; property of the machine, and quoting it as one would be wrong.
(define (bld-note-wide-boot!)
  (display (string-append
             "jolt build: note — the boot image is at or over Chez's "
             "LZ4 fasl ceiling;\n"
             "  re-encoding it with gzip (slower to decompress, but it loads)\n")))

(define (bld-note-retry-wide!)
  (display (string-append
             "jolt build: note — the boot image could not be written as an LZ4 "
             "vfasl entry;\n  retrying with gzip\n")))

(define (bld-note-no-vfasl!)
  (display (string-append
             "jolt build: note — the boot could not be converted to a vfasl "
             "image;\n  keeping the plain boot (slower to start, same "
             "behaviour)\n")))

(define (bld-note-unscannable-boot! boot)
  (display (string-append
             "jolt build: note — could not read the fasl entry headers of "
             boot ";\n  leaving its codec alone. If a large binary dies in "
             "Sbuild_heap reporting an\n  \"uncompressed size\", this is the "
             "check that stopped covering it.\n")))

;; Convert BOOT to vfasl at VBOOT in this process, answering whether VBOOT is
;; usable. LZ4 first — it is the fast default and what every normal image wants —
;; then gzip if the image cleared the ceiling. The second arm also covers the
;; conversion FAILING outright, which is what an image whose COMPRESSED half
;; clears the ceiling does: $bytevector-compress reports its length through the
;; same overflowing Sfixnum, so the write end raises long before the read end
;; would have.
(define (bld-vfasl-convert! boot vboot)
  (if (eq? (bld-boot-mode) 'small)
      (sa-vfasl-convert-file boot vboot 'wide)   ; asked for gzip; no ceiling to hit
      (if (and (sa-vfasl-convert-file boot vboot)
               (not (bld-boot-over-lz4-ceiling? vboot)))
          #t
          (and (sa-vfasl-convert-file boot vboot 'wide)
               (begin (bld-note-wide-boot!) #t)))))

;; The conversion as a form for the fresh-Chez compile scripts, empty under
;; 'plain. 'small sets the codec in that process the way sa-vfasl-convert-file's
;; 'wide does in this one.
;;
;; GUARDED, and it removes a half-written VBOOT on the way out. Those scripts run
;; under bld-system, which turns a non-zero exit into a dead build — so an
;; unguarded conversion means a target that cannot vfasl, or an image too big to
;; write as one entry, takes the whole build down instead of degrading to the
;; plain boot the way the in-process path does. bld-vfasl-ensure! reads the
;; presence of VBOOT as the verdict.
(define (bld-vfasl-script-form boot vboot)
  (if (bld-vfasl-disabled?)
      ""
      (string-append
        (if (eq? (bld-boot-mode) 'small) "(compress-format 'gzip)\n" "")
        "(guard (e (#t (when (file-exists? " (ei-str-lit vboot) ")\n"
        "                (delete-file " (ei-str-lit vboot) "))))\n"
        "  (vfasl-convert-file " (ei-str-lit boot) " " (ei-str-lit vboot) " '()))\n")))

;; One conversion in a fresh Chez, guarded the same way, leaving VBOOT present
;; only if it worked. CODEC is 'wide for gzip, anything else for the default.
(define (bld-vfasl-run-convert! builddir boot vboot codec)
  (when (file-exists? vboot) (delete-file vboot))
  (let ((cs (string-append builddir "/vfasl-convert.ss")))
    (let ((p (open-output-file cs 'replace)))
      (put-string p
        (string-append
          "(import (chezscheme))\n"
          (if (bld-cross?) (string-append "(load " (ei-str-lit (bld-xpatch)) ")\n") "")
          (if (eq? codec 'wide) "(compress-format 'gzip)\n" "")
          "(guard (e (#t (when (file-exists? " (ei-str-lit vboot) ")\n"
          "                (delete-file " (ei-str-lit vboot) "))))\n"
          "  (vfasl-convert-file " (ei-str-lit boot) " " (ei-str-lit vboot) " '()))\n"))
      (close-port p))
    (bld-system (string-append bld-chez " --script '" cs "'"))))

;; The fresh-Chez counterpart of bld-vfasl-convert!, and the same contract:
;; answer whether VBOOT is usable, never raise for a boot that simply could not
;; be imaged. The paths that convert inside their compile script (build-with-cc,
;; build-shared, and so every cross build) have to convert there — $fasl-to-vfasl
;; lays an image out for one machine, and a cross build needs the xpatch's
;; constants — so by the time we get here the attempt has already been made and
;; this only has to grade it:
;;
;;   nothing produced   the script's conversion raised. Retry under gzip, which
;;                      is also the arm for an image whose COMPRESSED half
;;                      clears the ceiling. Still nothing: keep the plain boot.
;;   over the ceiling   re-encode with gzip, as bld-vfasl-convert! does.
;;   otherwise          the LZ4 image stands.
(define (bld-vfasl-ensure! builddir boot vboot)
  (cond
    ((not (file-exists? vboot))
     (bld-note-retry-wide!)
     (bld-vfasl-run-convert! builddir boot vboot 'wide)
     (cond ((file-exists? vboot) #t)
           (else (bld-note-no-vfasl!) #f)))
    ((bld-boot-over-lz4-ceiling? vboot)
     (bld-note-wide-boot!)
     (bld-vfasl-run-convert! builddir boot vboot 'wide)
     (cond ((file-exists? vboot) #t)
           (else (bld-note-no-vfasl!) #f)))
    (else #t)))

;; jolt's OWN boot (build-jolt.ss), whose conversion is not guarded: a jolt build
;; that cannot image its boot is a broken toolchain, not a user's app degrading,
;; so this raises rather than quietly shipping a plain boot.
(define (bld-vfasl-regzip! builddir boot vboot)
  (when (bld-boot-over-lz4-ceiling? vboot)
    (bld-note-wide-boot!)
    (bld-vfasl-run-convert! builddir boot vboot 'wide)
    (unless (file-exists? vboot)
      (error 'jolt-build "gzip re-encode of the boot image failed" vboot))))

;; units: a list of (src so kind) compiled in order and loaded into the boot in
;; that order, so the runtime half's defines precede the app half's reads.
;;   'whole   — one unsplit flat file: kernel prologue + baked fingerprint, no cache
;;   'runtime — the app-independent half: same, plus the fasl cache
;;   'runtime-shaken — the same half with a tree-shaken core: per-app, no cache
;;   'app     — the app half: compiled plain. It needs no kernel prologue (its
;;              defines are jv$-munged and so cannot shadow a Chez name) and no
;;              fingerprint (the runtime unit carries the one that identifies it).
(define (build-self-contained entry-ns out-path mode builddir units boot native-link petite-only?)
  (let ((petite (string-append builddir "/petite.boot"))
        (scheme (string-append builddir "/scheme.boot")))
    (jolt-spill-embedded! "csv/petite.boot" petite)
    (unless petite-only? (jolt-spill-embedded! "csv/scheme.boot" scheme))
    (display (string-append "jolt build: compiling " entry-ns " (" mode " mode, self-contained)\n"))
    (for-each
      (lambda (u)
        (let ((src (car u)) (so (cadr u)) (kind (caddr u)))
          (case kind
            ((runtime) (bld-compile-runtime! src so #t))
            ((runtime-shaken) (bld-compile-runtime! src so #f))
            ((app)
             (bld-chez-compile-file mode src so)
             (ei-mark! "compile app half"))
            (else
             (bld-prepend-prologue! src)
             (ei-mark! "kernel prologue + hash")
             (bld-chez-compile-file mode src so)
             (ei-mark! "Chez compile-file")))))
      units)
    ;; A compiler-dropped binary (no runtime eval) boots from petite alone —
    ;; scheme.boot is the Chez compiler, ~5 MB of heap and ~1 MB of binary it
    ;; would never call. Chez's interpreter (petite) can't create a
    ;; foreign-procedure at runtime, but every defcfn in the image was
    ;; AOT-compiled, so the FFI is unaffected.
    ;; The unit fasls go in after the Chez boots, in the order they were compiled.
    (sa-make-boot-file boot
      (append (list petite)
              (if petite-only? '() (list scheme))
              (map cadr units)))
    (ei-mark! "make-boot-file")
    ;; vfasl: the same win jolt's own boot gets (build-jolt.ss) — the kernel loads
    ;; a prebuilt image straight into the static generation instead of walking a
    ;; fasl stream and allocating, and Sbuild_heap's Scompact_heap then has far
    ;; less to compact. Best effort: sa-vfasl-convert-file answers #f rather than
    ;; raising, and the plain boot that is already on disk stays the payload.
    ;;
    ;; NOT when cross-compiling. Unlike build-with-cc and build-shared, which run
    ;; their conversion inside the fresh-Chez compile script and so inherit the
    ;; xpatch's retargeted constants, this one runs in THIS process — the host's.
    ;; $fasl-to-vfasl lays the image out for a specific machine, so converting a
    ;; target's boot with host constants would produce a broken binary. A cross
    ;; build keeps the plain boot.
    (unless (or (bld-cross?) (bld-vfasl-disabled?))
      (let ((vboot (string-append boot ".vfasl")))
        (when (bld-vfasl-convert! boot vboot)
          (set! boot vboot)
          (ei-mark! "vfasl-convert"))))
    ;; The stub is the native launcher the boot is appended to. With no :static
    ;; natives it's the prebuilt one bundled in jolt (no cc needed); with :static
    ;; natives it's re-linked here from the bundled kernel + launcher source so the
    ;; archives are baked in and their symbols resolve in the running binary.
    (bld-clear-output! out-path)
    (if (> (string-length native-link) 0)
        (bld-relink-stub builddir native-link out-path)
        (jolt-spill-embedded! "stub/launcher" out-path))
    ;; link: stub bytes ++ boot ++ frame, then make it executable.
    (jolt-append-payload! out-path (read-file-bytes boot))
    (jolt-chmod-755 out-path)
    (ei-mark! "stub + payload link")
    (display (string-append "jolt build: wrote " out-path "\n"))
    (when bld-osx?
      (display (string-append
                 "jolt build: note — on macOS this binary is unsigned; to share it,\n"
                 "  `xattr -d com.apple.quarantine " out-path "` on the target, or sign it.\n")))))

;; Spill whichever bundled compression archives this binary carries into
;; BUILDDIR, and answer them as bld-bundled-archives expects. Empty for a jolt
;; built against a Chez that had none — bld-compression-lib then falls back and
;; warns, exactly as it would on the dev machine.
(define (bld-spill-bundled-archives builddir)
  (fold-left
    (lambda (acc lib)
      (let ((name (string-append "lib" lib ".a")))
        (if (jolt-embedded-bytes (string-append "csv/" name))
            (let ((path (string-append builddir "/" name)))
              (jolt-spill-embedded! (string-append "csv/" name) path)
              (cons (cons lib path) acc))
            acc)))
    '() '("lz4" "z")))

;; Re-link the launcher stub with the app's static native archives baked in, to
;; OUT-PATH. The self-contained jolt bundles the Chez kernel (libkernel.a),
;; header, and launcher source; spill them and drive the system cc — the same link
;; build-jolt.ss ran once at jolt-build time, plus the force-load archive flags
;; (native-link) and, on Linux, -rdynamic so the baked-in symbols stay dlsym-
;; visible for (load-shared-object #f) + foreign-procedure at startup.
(define (bld-relink-stub builddir native-link out-path)
  (let* ((h  (string-append builddir "/scheme.h"))
         (lk (string-append builddir "/libkernel.a"))
         (lc (string-append builddir "/launcher.c"))
         ;; The bundled lz4/zlib archives, spilled like the kernel: this link runs
         ;; on a machine with no Chez install, so bld-static-archive has nowhere
         ;; to look and the app would otherwise take whatever lz4 and zlib the
         ;; machine happens to have — runtime dependencies the appended-stub path
         ;; (the other 99% of builds) does not have, on a binary the user is
         ;; about to ship. An archive is absent only when the jolt running this
         ;; was itself built against a Chez that had none; then the link falls
         ;; back to -l as it always did.
         (archives (bld-spill-bundled-archives builddir)))
    (jolt-spill-embedded! "csv/scheme.h" h)
    (jolt-spill-embedded! "csv/libkernel.a" lk)
    (jolt-spill-embedded! "stub/launcher.c" lc)
    (bld-write-zlib-header! builddir)
    (display "jolt build: relinking launcher stub with static native libraries\n")
    (parameterize ((bld-bundled-archives archives))
      (bld-system (string-append
        "cc -O2 " (bld-export-symbols-flag)
        "-I'" builddir "' '" lc "' '" lk "' -o '" out-path "' "
        native-link " " (bld-link-libs))))))

;; --- boot-image prefetch (cold start) ---------------------------------------
;; A binary that embeds its boot as a C array hands Chez a pointer into .data — a
;; private, file-backed mapping the kernel demand-pages 4KB at a time as
;; Sbuild_heap walks it — and nothing tells the kernel that the whole multi-MB
;; range is about to be read in order. A cold jolt run reads 19.1MB of its 27.8MB
;; binary before it prints anything, nearly all of it this boot. MADV_WILLNEED
;; over the range, issued BEFORE Sscheme_init, lets that read overlap kernel init
;; and the runtime image's top levels instead of being scheduled fault by fault
;; behind them.
;;
;; It is a hint, and it buys nothing measurable on storage that is already
;; bandwidth-bound — the A/B is in the commit that added this. What it targets is
;; the opposite regime, a page-in bound by latency rather than throughput.
;; Advisory in every sense: nothing checks the result, no platform has to
;; implement it, and a failure costs the speedup and nothing else. Shared by the
;; three C-array boot sites — jolt's own main (build-jolt.ss), `jolt build`'s cc
;; executable, and --library. The appended-boot stub reads its boot through an fd
;; rather than a mapping and carries the fadvise-shaped equivalent itself
;; (stub/launcher.c).
(define (bld-boot-prefetch-defn)
  (string-append
    "#include <stddef.h>\n"
    "#if defined(__linux__) || defined(__APPLE__)\n"
    "#include <stdint.h>\n"
    "#include <sys/mman.h>\n"
    "#include <unistd.h>\n"
    "static void jolt_prefetch_boot(const void *p, size_t n) {\n"
    "  long pagesize = sysconf(_SC_PAGESIZE);\n"
    "  uintptr_t start, base;\n"
    "  if (pagesize <= 0 || n == 0) return;\n"
    "  /* madvise wants a page boundary; the array rarely starts on one. */\n"
    "  start = (uintptr_t)p;\n"
    "  base = start & ~(uintptr_t)(pagesize - 1);\n"
    "  madvise((void *)base, n + (size_t)(start - base), MADV_WILLNEED);\n"
    "}\n"
    "#else\n"
    "static void jolt_prefetch_boot(const void *p, size_t n) { (void)p; (void)n; }\n"
    "#endif\n"))

;; The call: the first statement of main / jolt_library_init, so the readahead is
;; already in flight for everything that follows it.
(define (bld-boot-prefetch-call)
  "  jolt_prefetch_boot(jolt_boot, (size_t)jolt_boot_len);\n")

;; --- legacy cc link (dev bin/jolt): fresh Chez compile + xxd + cc ------------
(define (build-with-cc entry-ns out-path mode builddir units boot boot-h main-c native-link petite-only?)
  (display (string-append "jolt build: compiling " entry-ns " (" mode " mode)\n"))
  (let ((cs (string-append builddir "/compile.ss")))
    (let ((p (open-output-file cs 'replace)))
      (put-string p
        (string-append
          "(import (chezscheme))\n"
          ;; cross: the xpatch retargets compile-file / make-boot-file to the
          ;; target machine (ChezScheme/BUILDING, "CROSS COMPILING SCHEME
          ;; PROGRAMS"); the boots below come from the target pack.
          (if (bld-cross?) (string-append "(load " (ei-str-lit (bld-xpatch)) ")\n") "")
          ;; each unit under its own parameters (see build-self-contained): the
          ;; runtime half without inspector information, the app half under the
          ;; mode's row. No kernel prologue here — a fresh Chez has nothing of
          ;; jolt's in its interaction environment to shadow a kernel name.
          (bld-units-compile-forms units mode)
          ;; petite-only boot when the compiler image was dropped (see
          ;; build-self-contained). The unit fasls follow the Chez boots in the
          ;; order they were compiled.
          "(make-boot-file " (ei-str-lit boot) " '()\n  "
          (ei-str-lit (string-append (bld-csv-dir) "/petite.boot")) "\n"
          (if petite-only?
              ""
              (string-append "  " (ei-str-lit (string-append (bld-csv-dir) "/scheme.boot")) "\n"))
          (bld-units-so-args units) ")\n"
          ;; vfasl, in THIS script so a cross build gets the xpatch's retargeted
          ;; constants the way make-boot-file above does — see build-jolt.ss.
          ;; --boot decides the codec, or omits the conversion (jolt#886).
          (bld-vfasl-script-form boot (string-append boot ".vfasl"))))
      (close-port p))
    (bld-system (string-append bld-chez " --script '" cs "'")))
  ;; the converted boot is what gets embedded
  ;; …and only switch to it if one actually exists: bld-vfasl-ensure! answers #f
  ;; for a target that could not be imaged at all, and the plain boot is then
  ;; what gets embedded.
  (unless (bld-vfasl-disabled?)
    (when (bld-vfasl-ensure! builddir boot (string-append boot ".vfasl"))
      (set! boot (string-append boot ".vfasl"))))
  (bld-system (string-append "xxd -i '" boot "' > '" boot-h "'"))
  ;; The xxd symbol is derived from the path; normalize to jolt_boot.
  (bld-system (string-append
    "sed -i.bak -E 's/unsigned char [A-Za-z0-9_]+\\[\\]/unsigned char jolt_boot[]/; "
    "s/unsigned int [A-Za-z0-9_]+_len/unsigned int jolt_boot_len/' '" boot-h "'"))
  (let ((mc (open-output-file main-c 'replace)))
    (put-string mc
      (string-append
        "#include \"scheme.h\"\n#include \"jolt_zlib.h\"\n#include \"boot_data.h\"\n"
        (bld-boot-prefetch-defn)
        "int main(int argc, char *argv[]) {\n"
        (bld-boot-prefetch-call)
        "  Sscheme_init(0);\n"
        "  Sregister_boot_file_bytes(\"jolt\", jolt_boot, jolt_boot_len);\n"
        "  Sbuild_heap(0, jolt_register_zlib);\n"
        "  int status = Sscheme_start(argc, (const char **)argv);\n"
        "  Sscheme_deinit();\n  return status;\n}\n"))
    (close-port mc))
  (bld-write-zlib-header! (path-parent main-c))
  ;; -rdynamic (Linux) exports the executable's symbols into the dynamic table so
  ;; a statically-linked native lib's symbols resolve via (load-shared-object #f)
  ;; at startup. macOS keeps unstripped executable symbols dlsym-visible already.
  (bld-clear-output! out-path)
  (bld-system (string-append
    (bld-cc) " " (bld-arch-flag) " -O2 " (if (> (string-length native-link) 0) (bld-export-symbols-flag) "")
    "-I'" (bld-csv-dir) "' '" main-c "' '" (bld-csv-dir) "/libkernel.a' "
    "-o '" out-path "' " native-link " " (bld-link-libs)))
  (display (string-append "jolt build: wrote " out-path "\n")))

;; --- shared-library link (jolt build --library) -----------------------------
;; The cc path adapted to emit a shared object instead of an executable: the same
;; compile-file + make-boot-file + xxd boot embedding, but a library.c stub
;; (jolt_library_init / jolt_lookup / jolt_library_shutdown instead of main) and
;; a -shared/-dynamiclib link. Only the cc path supports libraries today — the
;; self-contained append-to-prebuilt-stub path would need a library stub variant
;; baked into the distributed jolt (a follow-up).
;; last path segment of p (after the final '/'), for a dylib's -install_name.
(define (bld-basename p)
  (let loop ((i (fx- (string-length p) 1)))
    (cond ((fx<? i 0) p)
          ((char=? (string-ref p i) #\/) (substring p (fx+ i 1) (string-length p)))
          (else (loop (fx- i 1))))))

;; Write host/chez/stub/jolt_zlib.h into DIR, beside a generated or spilled C
;; file that #includes it. bld-source-string reads the binary's embedded copy
;; (build-jolt.ss registers it) and falls back to the checkout on disk.
(define (bld-write-zlib-header! dir)
  (let ((p (open-output-file (string-append dir "/jolt_zlib.h") 'replace)))
    (put-string p (bld-source-string "host/chez/stub/jolt_zlib.h"))
    (close-port p)))

(define (bld-library-stub)
  (string-append
    "#include \"scheme.h\"\n"
    "#include \"jolt_zlib.h\"\n"
    "#include <string.h>\n"
    "#include \"boot_data.h\"\n"
    (bld-boot-prefetch-defn)
    "/* jolt_set_lookup_addr is called from the built library's scheme-start\n"
    "   handler (registered via Sforeign_symbol after Sbuild_heap) to hand the\n"
    "   stub the Scheme lookup callable's address. */\n"
    "static void* (*jolt_lookup_fn)(const char*) = 0;\n"
    "void jolt_set_lookup_addr(void* fn) { jolt_lookup_fn = (void*(*)(const char*))fn; }\n"
    "void* jolt_lookup(const char* name) { return jolt_lookup_fn ? jolt_lookup_fn(name) : 0; }\n"
    "int jolt_library_init(int argc, char** argv) {\n"
    "  if (!argv) argc = 0;  /* Sscheme_start reads argv[0..argc-1]; a NULL argv means no args */\n"
    (bld-boot-prefetch-call)
    "  Sscheme_init(0);\n"
    "  Sregister_boot_file_bytes(\"jolt\", jolt_boot, (iptr)jolt_boot_len);\n"
    "  Sbuild_heap(0, jolt_register_zlib);\n"
    "  Sforeign_symbol(\"jolt_set_lookup_addr\", (void*)jolt_set_lookup_addr);\n"
    "  return Sscheme_start(argc, (const char**)argv); }\n"
    "void jolt_library_shutdown(void) { Sscheme_deinit(); }\n"))

;; The library scheme-start tail BODY: publish the export table to the embedder,
;; then return 0 so Sscheme_start returns to jolt_library_init's caller. The guard
;; (returning 1 on failure) is emitted by build-binary around the whole launcher —
;; prologue + this body — so an init failure anywhere reports and returns non-zero;
;; otherwise jolt_set_lookup_addr never runs and jolt_lookup silently returns NULL.
(define (bld-library-launcher-body)
  (string-append
    "      ;; publish the export table to the embedder\n"
    "      (let* ((lk (foreign-callable jolt-ffi-lookup-export (string) uptr))\n"
    "             (lk-addr (jolt-ffi-register-callable! lk)))\n"
    "        ((foreign-procedure \"jolt_set_lookup_addr\" (void*) void) lk-addr))\n"
    "      0)))\n"))

(define (build-shared entry-ns out-path mode builddir units boot boot-h native-link)
  (display (string-append "jolt build: compiling " entry-ns " (" mode " mode, shared library)\n"))
  (let ((cs (string-append builddir "/compile.ss")))
    (let ((p (open-output-file cs 'replace)))
      (put-string p
        (string-append
          "(import (chezscheme))\n"
          ;; As in build-with-cc, loading the xpatch retargets compile-file and
          ;; make-boot-file for the lifetime of this fresh Chez process.
          (if (bld-cross?) (string-append "(load " (ei-str-lit (bld-xpatch)) ")\n") "")
          (bld-units-compile-forms units mode)
          "(make-boot-file " (ei-str-lit boot) " '()\n  "
          (ei-str-lit (string-append (bld-csv-dir) "/petite.boot")) "\n  "
          (ei-str-lit (string-append (bld-csv-dir) "/scheme.boot")) "\n"
          (bld-units-so-args units) ")\n"
          ;; vfasl, as in build-with-cc and build-jolt.ss
          (bld-vfasl-script-form boot (string-append boot ".vfasl"))))
      (close-port p))
    (bld-system (string-append bld-chez " --script '" cs "'")))
  ;; …and only switch to it if one actually exists: bld-vfasl-ensure! answers #f
  ;; for a target that could not be imaged at all, and the plain boot is then
  ;; what gets embedded.
  (unless (bld-vfasl-disabled?)
    (when (bld-vfasl-ensure! builddir boot (string-append boot ".vfasl"))
      (set! boot (string-append boot ".vfasl"))))
  (bld-system (string-append "xxd -i '" boot "' > '" boot-h "'"))
  (bld-system (string-append
    "sed -i.bak -E 's/unsigned char [A-Za-z0-9_]+\\[\\]/unsigned char jolt_boot[]/; "
    "s/unsigned int [A-Za-z0-9_]+_len/unsigned int jolt_boot_len/' '" boot-h "'"))
  (let ((lc (string-append builddir "/library.c")))
    (let ((p (open-output-file lc 'replace)))
      (put-string p (bld-library-stub))
      (close-port p))
    (bld-clear-output! out-path)
    (bld-write-zlib-header! builddir)
    (bld-system (string-append
      (bld-cc) " " (bld-arch-flag) " -O2 -fPIC "
      ;; -install_name @rpath/<base> so a binary that link-edits against the dylib
      ;; (rather than dlopen'ing it) can locate it via its rpath, not a build-dir path.
      (if (bld-tgt-osx?)
          (string-append "-dynamiclib -install_name '@rpath/" (bld-basename out-path) "' ")
          "-shared ")
      "-I'" (bld-csv-dir) "' '" lc "' '" (bld-csv-dir) "/libkernel.a' "
      "-o '" out-path "' " native-link " " (bld-link-libs))))
  (display (string-append "jolt build: wrote " out-path "\n")))

;; optional trailing (target target-pack boot-mode allow-dynamic): a Chez machine
;; string + a prepared target pack dir when cross-compiling (jolt build --target)
;; — absent/nil = host — then the boot mode (bld-opt-boot-mode) and the
;; :allow-dynamic list (bld-opt-strs).
(define (bld-opt-str opt i)
  (let loop ((o opt) (i i))
    (cond ((or (null? o) (< i 0)) #f)
          ((= i 0) (and (not (jolt-nil? (car o))) (jolt-str-render-one (car o))))
          (else (loop (cdr o) (- i 1))))))
;; The boot mode as a symbol. Absent (a caller passing only the cross pair) or
;; unrecognized reads as the default, so a bad value degrades to today's build
;; rather than failing one; jolt.main is what rejects a typo, with a message.
(define (bld-opt-boot-mode opt i)
  (let ((s (bld-opt-str opt i)))
    (cond ((equal? s "small") 'small)
          ((equal? s "plain") 'plain)
          (else 'fast))))
;; optional trailing (allow-dynamic), index 3: a vector of "ns/name" strings
;; (see build-binary). Absent/nil = '().
(define (bld-opt-strs opt i)
  (let loop ((o opt) (i i))
    (cond ((or (null? o) (< i 0)) '())
          ((= i 0) (if (jolt-nil? (car o)) '() (bld-strs (car o))))
          (else (loop (cdr o) (- i 1))))))
(def-var! "jolt.host" "build-binary"
  (lambda (entry out mode natives embed-dirs ext-roots direct-link? tree-shake? . opt)
    (parameterize ((bld-target (bld-opt-str opt 0)) (bld-target-pack (bld-opt-str opt 1))
                   (bld-boot-mode (bld-opt-boot-mode opt 2)))
      (build-binary (jolt-str-render-one entry)
                    (jolt-str-render-one out)
                    (jolt-str-render-one mode)
                    natives embed-dirs ext-roots (jolt-truthy? direct-link?) (jolt-truthy? tree-shake?)
                    (bld-opt-strs opt 3) #f))
    jolt-nil))
(def-var! "jolt.host" "build-library"
  (lambda (entry out mode natives embed-dirs ext-roots direct-link? tree-shake? . opt)
    (parameterize ((bld-target (bld-opt-str opt 0)) (bld-target-pack (bld-opt-str opt 1))
                   (bld-boot-mode (bld-opt-boot-mode opt 2)))
      (build-binary (jolt-str-render-one entry)
                    (jolt-str-render-one out)
                    (jolt-str-render-one mode)
                    natives embed-dirs ext-roots (jolt-truthy? direct-link?) (jolt-truthy? tree-shake?)
                    (bld-opt-strs opt 3) #t))
    jolt-nil))
